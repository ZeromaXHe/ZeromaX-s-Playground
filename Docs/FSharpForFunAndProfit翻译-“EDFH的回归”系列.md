# [è¿”å›ä¸» Markdown](./FSharpForFunAndProfitç¿»è¯‘.md)



# 1 ä¼ä¸šå¼€å‘è€…ä»åœ°ç‹±å½’æ¥

*Part of the "The Return of the EDFH" series (*[link](https://fsharpforfunandprofit.com/posts/return-of-the-edfh/#series-toc)*)*

æ›´å¤šçš„æ¶æ„åˆè§„æ€§ï¼Œæ›´å¤šçš„åŸºäºå±æ€§çš„æµ‹è¯•
2021å¹´2æœˆ14æ—¥

https://fsharpforfunandprofit.com/posts/return-of-the-edfh/

åœ¨[ä¹‹å‰çš„ä¸€ç³»åˆ—å¸–å­](https://fsharpforfunandprofit.com/pbt)ä¸­ï¼Œæˆ‘å‘æ‚¨ä»‹ç»äº†ä¸€ä½ç²¾ç–²åŠ›ç«­ã€æ‡’æƒ°çš„ç¨‹åºå‘˜ï¼Œä»–è¢«ç§°ä¸º*åœ°ç‹±ä¼ä¸šå¼€å‘äººå‘˜*ï¼ˆ*Enterprise Developer From Hell*ï¼‰ï¼Œç®€ç§° EDFHã€‚æ­£å¦‚æˆ‘ä»¬æ‰€è§ï¼ŒEDFH å–œæ¬¢[æ¶æ„éµå®ˆè§„å®š](https://www.reddit.com/r/MaliciousCompliance/top/?sort=top&t=all)ã€‚

æœ€è¿‘ï¼ŒEDFH å¯¹ä¸€ä¸ª[é‡‡è®¿é—®é¢˜çš„ç—…æ¯’å¼å›ç­”](https://twitter.com/allenholub/status/1357115515672555520)æ˜æ˜¾äº§ç”Ÿäº†å½±å“ã€‚

> ç¼–å†™ä¸€ä¸ªå°†è¾“å…¥è½¬æ¢ä¸ºè¾“å‡ºçš„å‡½æ•°ã€‚
>
> è¾“å…¥ï¼šâ€œaaaabbbccaâ€
> è¾“å‡ºï¼š[ï¼ˆaâ€™ï¼Œ4ï¼‰ï¼Œï¼ˆbâ€™ï¼Œ3ï¼‰ï¼Œï¼ˆcâ€™ï¼Œ2ï¼‰ï¼Œï¼ˆaâ€™â€™ï¼Œ1ï¼‰]

å½“ç„¶ï¼ŒEDFH çš„ç­”æ¡ˆå¾ˆç®€å•ï¼š

```F#
let func inputStr =
  // hard code the answer
  [('a',4); ('b',3); ('c',2); ('a',1)]
```

å› ä¸ºè¿™æ˜¯æˆ‘ä»¬å¾—åˆ°çš„å”¯ä¸€è§„èŒƒï¼Œæ‰€ä»¥è¿™æ˜¯ä¸€ä¸ªå®Œç¾çš„å®ç°ï¼

ä¸è¿‡ï¼Œè¿™å¾ˆæœ‰è¶£ï¼Œå› ä¸ºé¢è¯•å®˜æ˜¾ç„¶è¦æ±‚æ›´å¤æ‚çš„ä¸œè¥¿ã€‚

ä½†è¿™å¼•å‘äº†ä¸¤ä¸ªéå¸¸é‡è¦çš„é—®é¢˜ï¼šé¢è¯•å®˜åˆ°åº•åœ¨é—®ä»€ä¹ˆï¼Ÿä»–ä»¬æ€ä¹ˆçŸ¥é“ä»–ä»¬æ˜¯å¦å¾—åˆ°äº†å®ƒï¼Ÿ

åªæœ‰ä¸€ä¸ªè¾“å…¥/è¾“å‡ºå¯¹ï¼Œæœ‰å¾ˆå¤šæ½œåœ¨çš„è§„èŒƒå¯ä»¥å·¥ä½œã€‚ç„¶è€Œï¼Œtwitter çš„å…±è¯†æ˜¯ï¼Œè¿™æ˜¯ä¸€ä¸ªè¡Œç¨‹ç¼–ç ï¼ˆrun-length encoding, RLEï¼‰é—®é¢˜ã€‚ğŸ˜€

æ‰€ä»¥ç°åœ¨æˆ‘ä»¬æœ‰ä¸¤ä¸ªå…·ä½“çš„æŒ‘æˆ˜ï¼š

- RLE çš„è§„æ ¼åº”è¯¥æ˜¯ä»€ä¹ˆï¼Ÿæˆ‘ä»¬å¦‚ä½•æ˜ç¡®åœ°å®šä¹‰å®ƒï¼Ÿ
- æˆ‘ä»¬å¦‚ä½•æ£€æŸ¥ç‰¹å®šçš„ RLE å®ç°æ˜¯å¦ç¬¦åˆè¯¥è§„èŒƒï¼Ÿ

é‚£ä¹ˆ RLE çš„è§„æ ¼åˆ°åº•æ˜¯ä»€ä¹ˆæ ·å­çš„å‘¢ï¼Ÿæœ‰è¶£çš„æ˜¯ï¼Œå½“æˆ‘åœ¨äº’è”ç½‘ä¸Šæœç´¢æ—¶ï¼Œæˆ‘æ²¡æœ‰æ‰¾åˆ°å¤ªå¤šã€‚ç»´åŸºç™¾ç§‘æœ‰ä¸€ä¸ª [RLE é¡µé¢](https://en.wikipedia.org/wiki/Run-length_encoding)ï¼Œä¸Šé¢æœ‰ä¸€ä¸ªç¤ºä¾‹ï¼Œä½†æ²¡æœ‰è§„èŒƒã€‚Rosetta Stone æœ‰ä¸€ä¸ªå¸¦æœ‰éæ­£å¼è§„èŒƒçš„ [RLE é¡µé¢](https://rosettacode.org/wiki/Run-length_encoding)ã€‚

## åœ¨ EDFH å­˜åœ¨çš„æƒ…å†µä¸‹è¿›è¡Œæµ‹è¯•

è®©æˆ‘ä»¬æŠŠè§„æ ¼æç½®ä¸€åˆ†é’Ÿï¼ŒæŠŠæ³¨æ„åŠ›è½¬å‘æµ‹è¯•ã€‚æˆ‘ä»¬å¦‚ä½•æ£€æŸ¥ RLE å®ç°æ˜¯å¦æœ‰æ•ˆï¼Ÿ

ä¸€ç§æ–¹æ³•æ˜¯è¿›è¡ŒåŸºäºç¤ºä¾‹çš„æµ‹è¯•ï¼š

- é¢„æœŸ `rle("")` çš„è¾“å‡ºä¸º `[]`
- é¢„æœŸ `rle("a")` çš„è¾“å‡ºä¸º `[(a,1)]`
- é¢„æœŸ `rle("aab")` çš„è¾“å‡ºä¸º `[(a,2); (b,1)]`

ç­‰ç­‰ã€‚

ä½†æ˜¯ï¼Œå¦‚æœæˆ‘ä»¬å›é¡¾ä¸€ä¸‹æˆ‘ä»¬ä¹‹å‰åœ¨ EDFH æ–¹é¢çš„ç»éªŒï¼Œä»–ä»¬è‚¯å®šä¼šå‘ç°ä¸€ä¸ªé€šè¿‡æ‰€æœ‰æµ‹è¯•çš„å®ç°ï¼Œä½†ä»ç„¶æ˜¯é”™è¯¯çš„ã€‚ä¾‹å¦‚ï¼ŒEDFH å¯¹ä¸Šè¿°ç¤ºä¾‹çš„å®ç°å¯èƒ½å¦‚ä¸‹ï¼š

```F#
let rle inputStr =
  match inputStr with
  | "" ->
    []
  | "a" ->
    [('a',1)]
  | "aab" ->
    [('a',2); ('b',1)]
  | "aaaabbbcca" ->
    [('a',4); ('b',3); ('c',2); ('a',1)]
  // everything else
  | _ -> []
```

å¦‚æœæˆ‘ä»¬æ£€æŸ¥ä¸€ä¸‹è¿™ä¸ªå®ç°ï¼Œå®ƒçœ‹èµ·æ¥ç›¸å½“ä¸é”™ï¼

```F#
rle "a"           //=> [('a',1);]
rle "aab"         //=> [('a',2); ('b',1)]
rle "aaaabbbcca"  //=> [('a',4); ('b',3); ('c',2); ('a',1)]
```

å‡»è´¥ EFDH çš„æœ€ä½³æ–¹æ³•æ˜¯ä½¿ç”¨éšæœºè¾“å…¥ï¼Œç‰¹åˆ«æ˜¯åŸºäºå±æ€§çš„æµ‹è¯•ã€‚

åŸºäºå±æ€§çš„æµ‹è¯•çš„ä¸€ä¸ªå¥½å¤„æ˜¯ï¼Œé€šè¿‡è¿™æ ·åšï¼Œæ‚¨é€šå¸¸å¯ä»¥å‘ç°è§„èŒƒã€‚åœ¨[ä¸Šä¸€ç¯‡æ–‡ç« ](https://fsharpforfunandprofit.com/posts/property-based-testing)ä¸­ï¼Œæˆ‘è®¨è®ºäº†å¦‚ä½•æµ‹è¯•åŠ æ³•çš„å®ç°ã€‚æœ€ç»ˆï¼Œæˆ‘ä»¬å‘ç°äº†äº¤æ¢æ€§ã€ç»“åˆæ€§å’Œæ’ç­‰æ€§çš„æ€§è´¨ã€‚è¿™äº›ä¸ä»…å®šä¹‰äº†æˆ‘ä»¬éœ€è¦çš„æµ‹è¯•ï¼Œè¿˜å‡ ä¹å®šä¹‰äº†â€œæ·»åŠ â€çš„å®é™…å«ä¹‰ã€‚

è®©æˆ‘ä»¬çœ‹çœ‹æˆ‘ä»¬æ˜¯å¦å¯ä»¥ä¸º RLE åšåŒæ ·çš„äº‹æƒ…ã€‚

## ä½¿ç”¨ EDFH å®ç°å¸®åŠ©æˆ‘ä»¬æ€è€ƒå±æ€§

è¯·è®°ä½ï¼Œåœ¨åŸºäºå±æ€§çš„æµ‹è¯•ä¸­ï¼Œæˆ‘ä»¬ä¸å…è®¸é‡æ–°å®ç°é€»è¾‘ï¼Œè€Œæ˜¯å¿…é¡»æå‡ºé€‚ç”¨äºæ‰€æœ‰è¾“å…¥çš„é€šç”¨å±æ€§ã€‚

ä½†è¿™æ˜¯æœ€éš¾çš„éƒ¨åˆ†â€”â€”æ€è€ƒå±æ€§ã€‚ä½†æ˜¯ï¼Œæˆ‘ä»¬å¯ä»¥ä½¿ç”¨ EDFH æ¥æŒ‡å¯¼æˆ‘ä»¬ï¼å¯¹äº EDFH åˆ›å»ºçš„æ¯ä¸ªå®ç°ï¼Œæˆ‘ä»¬æ‰¾å‡ºé”™è¯¯çš„åŸå› ï¼Œç„¶ååˆ›å»ºä¸€ä¸ªå±æ€§æ¥æ•è·è¯¥éœ€æ±‚ã€‚

ä¾‹å¦‚ï¼ŒEDFH å¯èƒ½ä¼šå°† RLE å‡½æ•°å®ç°ä¸ºç©ºåˆ—è¡¨ï¼Œè€Œä¸ç®¡è¾“å…¥æ˜¯ä»€ä¹ˆï¼š

```F#
let rle_empty (inputStr:string) : (char*int) list =
  []
```

ä¸ºä»€ä¹ˆè¿™æ˜¯é”™çš„ï¼Ÿå› ä¸ºè¾“å‡ºå¿…é¡»ä¸è¾“å…¥æœ‰æŸç§è¿æ¥ã€‚äº‹å®ä¸Šï¼Œå®ƒåº”è¯¥åŒ…å«è¾“å…¥ä¸­çš„æ¯ä¸ªå­—ç¬¦ã€‚

é‚£ä¹ˆï¼ŒEDFH å°†é€šè¿‡è¿”å›æ¯ä¸ªå­—ç¬¦å¹¶è®¡æ•°1æ¥è¿›è¡ŒæŠ¥å¤ã€‚

```F#
let rle_allChars inputStr =
  inputStr
  |> Seq.toList
  |> List.map (fun ch -> (ch,1))
```

å¦‚æœæˆ‘ä»¬è¿è¡Œè¿™ä¸ªï¼Œæˆ‘ä»¬å¾—åˆ°

```F#
rle_allChars ""      //=> []
rle_allChars "a"     //=> [('a',1)]
rle_allChars "abc"   //=> [('a',1); ('b',1); ('c',1)]
rle_allChars "aab"   //=> [('a',1); ('a',1); ('b',1)]
```

è¿™äº›è¾“å‡ºç¡®å®åŒ…å«äº†ç›¸åº”è¾“å…¥ä¸­çš„æ¯ä¸ªå­—ç¬¦ã€‚

ä¸ºä»€ä¹ˆè¿™æ˜¯é”™çš„ï¼Ÿå¥½å§ï¼Œæˆ‘ä»¬æƒ³æ”¶é›†â€œåˆ†æ•°â€ï¼Œè¿™æ„å‘³ç€æˆ‘ä»¬ä¸åº”è¯¥åœ¨ä¸€èµ·æœ‰ä¸¤ä¸ª aã€‚è¾“å‡ºåˆ—è¡¨ä¸­çš„æ¯ä¸ªå­—ç¬¦å¿…é¡»ä¸ç›¸é‚»å­—ç¬¦ä¸åŒã€‚

è¿™å¯¹ EFDH æ¥è¯´æ˜¯ä¸€ä¸ªç®€å•çš„ä¿®å¤ï¼Œåªéœ€åœ¨ç®¡é“ä¸­æ·»åŠ  `distinct`ï¼

```F#
let rle_distinct inputStr =
  inputStr
  |> Seq.distinct // added
  |> Seq.toList
  |> List.map (fun ch -> (ch,1))
```

ç°åœ¨è¾“å‡ºæ»¡è¶³â€œrunsâ€å±æ€§â€”â€”é‡å¤é¡¹å·²ç»æ¶ˆå¤±ã€‚

```F#
rle_distinct "a"     //=> [('a',1)]
rle_distinct "aab"   //=> [('a',1); ('b',1))]
rle_distinct "aaabb" //=> [('a',1); ('b',1))]
```

ä¸ºä»€ä¹ˆè¿™æ˜¯é”™çš„ï¼Ÿé‚£ä¹ˆè¿™äº›æ•°å­—å‘¢ï¼Ÿä»–ä»¬éƒ½åªæ˜¯ 1ã€‚å®ƒä»¬åº”è¯¥æ˜¯ä»€ä¹ˆï¼Ÿ

å¦‚æœä¸é‡æ–°å®ç°ç®—æ³•ï¼Œæˆ‘ä»¬ä¸çŸ¥é“å„ä¸ªè®¡æ•°åº”è¯¥æ˜¯å¤šå°‘ï¼Œä½†æˆ‘ä»¬çŸ¥é“å®ƒä»¬åº”è¯¥åŠ èµ·æ¥æ˜¯å¤šå°‘ï¼šå­—ç¬¦ä¸²ä¸­çš„å­—ç¬¦æ•°ã€‚å¦‚æœæºå­—ç¬¦ä¸²ä¸­æœ‰ 5 ä¸ªå­—ç¬¦ï¼Œåˆ™æ¸¸ç¨‹é•¿åº¦çš„æ€»å’Œä¹Ÿåº”è¯¥æ˜¯ 5ã€‚

ä¸å¹¸çš„æ˜¯ï¼ŒEDFH å¯¹æ­¤ä¹Ÿæœ‰ç­”æ¡ˆã€‚å®ƒä»¬çš„å®ç°åªèƒ½ä½¿ç”¨ `groupBy` æˆ– `countBy` æ¥è·å–è®¡æ•°ã€‚

```F#
let rle_groupedCount inputStr =
  inputStr
  |> Seq.countBy id
  |> Seq.toList
```

ä¹ä¸€çœ‹ï¼Œè¾“å‡ºæ•ˆæœä¸é”™

```F#
rle_groupedCount "aab"         //=> [('a',2); ('b',1))]
rle_groupedCount "aaabb"       //=> [('a',3); ('b',3))]
rle_groupedCount "aaaabbbcca"  //=> [('a',5); ('b',3); ('c',2))]
```

ä½†æœ‰ä¸€ä¸ªå¾®å¦™çš„é—®é¢˜ã€‚åœ¨ç¬¬ä¸‰ä¸ªç¤ºä¾‹ä¸­ï¼Œ`'a'` æœ‰ä¸¤ä¸ªä¸åŒçš„è¿è¡Œï¼Œä½† `rle_groupedCount` å®ç°å°†å®ƒä»¬åˆå¹¶åœ¨ä¸€èµ·ã€‚

æˆ‘ä»¬æƒ³è¦çš„ï¼š

```F#
[('a',4); ('b',3); ('c',2); ('a',1)]
```

æˆ‘ä»¬å¾—åˆ°äº†ä»€ä¹ˆï¼š

```F#
[('a',5); ('b',3); ('c',2)]
//    ^ wrong number      ^ another entry needed here
```

`groupedCount` æ–¹æ³•çš„é—®é¢˜åœ¨äºå®ƒæ²¡æœ‰è€ƒè™‘å­—ç¬¦çš„é¡ºåºã€‚æˆ‘ä»¬èƒ½æƒ³å‡ºä»€ä¹ˆæ ·çš„å±æ€§æ¥æ•æ‰è¿™ç§æƒ…å†µï¼Ÿ

æ£€æŸ¥æ’åºçš„æœ€ç®€å•æ–¹æ³•å°±æ˜¯åè½¬æŸäº›å†…å®¹ï¼åœ¨è¿™ç§æƒ…å†µä¸‹ï¼Œæˆ‘ä»¬å¯ä»¥æœ‰ä¸€ä¸ªå±æ€§ï¼šâ€œåå‘è¾“å…¥åº”è¯¥ç»™å‡ºåå‘è¾“å‡ºâ€ã€‚`rle_groupedCount` å®ç°ä¼šå¤±è´¥â€”â€”è¿™æ­£æ˜¯æˆ‘ä»¬æƒ³è¦çš„ã€‚

å› æ­¤ï¼Œåªéœ€å‡ åˆ†é’Ÿçš„æ€è€ƒï¼ˆä»¥åŠ EDFH çš„ä¸€äº›å¸®åŠ©ï¼‰ï¼Œæˆ‘ä»¬å°±å¯ä»¥ä½¿ç”¨ä¸€äº›å±æ€§æ¥æ£€æŸ¥ RLE å®ç°ï¼š

- è¾“å‡ºå¿…é¡»åŒ…å«è¾“å…¥ä¸­çš„æ‰€æœ‰å­—ç¬¦
- è¾“å‡ºä¸­æ²¡æœ‰ä¸¤ä¸ªç›¸é‚»çš„å­—ç¬¦å¯ä»¥ç›¸åŒ
- è¾“å‡ºä¸­çš„è¡Œç¨‹é•¿åº¦ä¹‹å’Œå¿…é¡»ç­‰äºè¾“å…¥çš„æ€»é•¿åº¦
- å¦‚æœè¾“å…¥è¢«åè½¬ï¼Œè¾“å‡ºä¹Ÿå¿…é¡»è¢«åè½¬

> è¿™è¶³ä»¥æ­£ç¡®æ£€æŸ¥ RLE å®ç°å—ï¼Ÿä½ èƒ½æƒ³åˆ°ä»»ä½•æ»¡è¶³è¿™äº›å±æ€§ä½†é”™è¯¯çš„æ¶æ„ EDFH å®ç°å—ï¼Ÿæˆ‘ä»¬å°†åœ¨[ç¨åçš„å¸–å­](https://fsharpforfunandprofit.com/posts/return-of-the-edfh-3)ä¸­é‡æ–°è®¨è®ºè¿™ä¸ªé—®é¢˜ã€‚	

## å±æ€§æ£€æŸ¥å®è·µ

è®©æˆ‘ä»¬æŠŠè¿™äº›æ¦‚å¿µä»˜è¯¸å®è·µã€‚æˆ‘ä»¬å°†ä½¿ç”¨ F# åº“ `FsCheck` æ¥æµ‹è¯•è¿™äº›å±æ€§ï¼ŒåŒ…æ‹¬å¥½çš„å’Œåçš„å®ç°ã€‚

ä» F# 5 å¼€å§‹ï¼Œå¾ˆå®¹æ˜“å°† FsCheck åŠ è½½åˆ°äº¤äº’å¼å·¥ä½œåŒºä¸­ã€‚ä½ å¯ä»¥åƒè¿™æ ·ç›´æ¥å¼•ç”¨å®ƒï¼š

```F#
#r "nuget:FsCheck"
```

*æ³¨æ„ï¼šæœ‰å…³è¿™äº›ç¤ºä¾‹ä¸­ä½¿ç”¨çš„ä»£ç ï¼Œè¯·å‚é˜…æœ¬æ–‡åº•éƒ¨çš„é“¾æ¥*

ç°åœ¨æˆ‘ä»¬å¯ä»¥ç¼–å†™ç¬¬ä¸€ä¸ªå±æ€§ï¼šâ€œç»“æœå¿…é¡»åŒ…å«è¾“å…¥ä¸­çš„æ‰€æœ‰å­—ç¬¦â€

```F#
// An RLE implementation has this signature
type RleImpl = string -> (char*int) list

let propUsesAllCharacters (impl:RleImpl) inputStr =
  let output = impl inputStr
  let expected =
    inputStr
    |> Seq.distinct
    |> Seq.toList
  let actual =
    output
    |> Seq.map fst
    |> Seq.distinct
    |> Seq.toList
  expected = actual
```

é€šå¸¸ï¼Œå±æ€§çš„å”¯ä¸€å‚æ•°æ˜¯è¢«æµ‹è¯•çš„å‚æ•°ï¼Œä½†åœ¨è¿™ç§æƒ…å†µä¸‹ï¼Œæˆ‘ä»¬è¿˜å°†ä¼ é€’ä¸€ä¸ªå®ç°å‚æ•°ï¼Œä»¥ä¾¿æˆ‘ä»¬å¯ä»¥ä½¿ç”¨EDFHå®ç°ä»¥åŠï¼ˆå¸Œæœ›ï¼‰æ­£ç¡®çš„å®ç°è¿›è¡Œæµ‹è¯•

### æ£€æŸ¥ rle_empty å®ç°

è®©æˆ‘ä»¬ç”¨ç¬¬ä¸€ä¸ª EDFH å®ç°æ¥å°è¯•ä¸€ä¸‹ï¼Œè¿™ä¸ªå®ç°æ€»æ˜¯è¿”å›ç©ºåˆ—è¡¨ï¼š

```F#
let impl = rle_empty
let prop = propUsesAllCharacters impl
FsCheck.Check.Quick prop
```

FsCheck çš„å›åº”æ˜¯ï¼š

```
Falsifiable, after 1 test (1 shrink) (StdGen (777291017, 296855223)):
Original:
"#"
Shrunk:
"a"
```

æ¢å¥è¯è¯´ï¼Œç®€å•åœ°ä½¿ç”¨æœ€å°å­—ç¬¦ä¸²â€œaâ€ä½œä¸ºè¾“å…¥å°†ç ´åè¯¥å±æ€§ã€‚

### æ£€æŸ¥ rle_allChars çš„å®ç°

å¦‚æœæˆ‘ä»¬å°è¯•ä½¿ç”¨ `rle_allChars` å®ç°â€¦

```F#
let impl = rle_allChars
let prop = propUsesAllCharacters impl
FsCheck.Check.Quick prop
```

â€¦æˆ‘ä»¬ç«‹å³å¾—åˆ° `ArgumentNullException`ï¼Œå› ä¸ºæˆ‘ä»¬å®Œå…¨å¿˜è®°äº†åœ¨å®ç°ä¸­å¤„ç† null è¾“å…¥ï¼è°¢è°¢ä½ ï¼ŒåŸºäºå±æ€§çš„æµ‹è¯•ï¼

è®©æˆ‘ä»¬ä¿®å¤å®ç°ä»¥å¤„ç† nullâ€¦

```F#
let rle_allChars inputStr =
  // add null check
  if System.String.IsNullOrEmpty inputStr then
    []
  else
    inputStr
    |> Seq.toList
    |> List.map (fun ch -> (ch,1))
```

â€¦ç„¶åå†è¯•ä¸€æ¬¡â€”â€”å“å‘€â€”â€”æˆ‘ä»¬åˆé‡åˆ°äº†ä¸€ä¸ªç©ºé—®é¢˜ï¼Œè¿™æ¬¡æ˜¯åœ¨æˆ‘ä»¬çš„å±æ€§ä¸­ã€‚æˆ‘ä»¬ä¹Ÿæ¥è§£å†³è¿™ä¸ªé—®é¢˜ã€‚

```F#
let propUsesAllCharacters (impl:RleImpl) inputStr =
  let output = impl inputStr
  let expected =
    if System.String.IsNullOrEmpty inputStr then
      []
    else
      inputStr
      |> Seq.distinct
      |> Seq.toList
  let actual =
    output
    |> Seq.map fst
    |> Seq.distinct
    |> Seq.toList
  expected = actual
```

ç°åœ¨ï¼Œå¦‚æœæˆ‘ä»¬å†è¯•ä¸€æ¬¡ï¼Œå±æ€§å°±é€šè¿‡äº†ã€‚

`å¥½çš„ï¼Œé€šè¿‡äº† 100 æ¬¡æµ‹è¯•ã€‚`

å› æ­¤ï¼Œæ­£å¦‚æˆ‘ä»¬æ‰€æ–™ï¼Œä¸æ­£ç¡®çš„ `rle_allChars` å®ç°ç¡®å®é€šè¿‡äº†ã€‚è¿™å°±æ˜¯ä¸ºä»€ä¹ˆæˆ‘ä»¬éœ€è¦ä¸‹ä¸€ä¸ªå±æ€§ï¼šâ€œè¾“å‡ºä¸­çš„ç›¸é‚»å­—ç¬¦ä¸èƒ½ç›¸åŒâ€

## â€œç›¸é‚»å­—ç¬¦ä¸ç›¸åŒâ€å±æ€§

ä¸ºäº†å®šä¹‰æ­¤å±æ€§ï¼Œæˆ‘ä»¬å°†é¦–å…ˆå®šä¹‰ä¸€ä¸ªè¾…åŠ©å‡½æ•° `removeDupAdjacentChars`ï¼Œç”¨äºåˆ é™¤é‡å¤é¡¹ã€‚

```F#
/// Given a list of elements, remove elements that have the
/// same char as the preceding element.
/// Example:
///   removeDupAdjacentChars ['a';'a';'b';'b';'a'] => ['a'; 'b'; 'a']
let removeDupAdjacentChars charList =
  let folder stack element =
    match stack with
    | [] ->
      // First time? Create the stack
      [element]
    | top::_ ->
      // New element? add it to the stack
      if top <> element then
        element::stack
      // else leave stack alone
      else
        stack

  // Loop over the input, generating a list of non-dup items.
  // These are in reverse order. so reverse the result
  charList |> List.fold folder [] |> List.rev
```

æœ‰äº†è¿™ä¸ªï¼Œæˆ‘ä»¬çš„å±æ€§å°†ä»è¾“å‡ºä¸­è·å–å­—ç¬¦ï¼Œç„¶ååˆ é™¤é‡å¤å­—ç¬¦ã€‚å¦‚æœå®ç°æ­£ç¡®ï¼Œåˆ é™¤é‡å¤é¡¹åº”è¯¥æ²¡æœ‰ä»»ä½•æ•ˆæœã€‚

```F#
/// Property: "Adjacent characters in the output cannot be the same"
let propAdjacentCharactersAreNotSame (impl:RleImpl) inputStr =
  let output = impl inputStr
  let actual =
    output
    |> Seq.map fst
    |> Seq.toList
  let expected =
    actual
    |> removeDupAdjacentChars // should have no effect
  expected = actual // should be the same
```

ç°åœ¨ï¼Œè®©æˆ‘ä»¬å¯¹ç…§ EDFH çš„ `rle_allChars` å®ç°æ¥æ£€æŸ¥è¿™ä¸ªæ–°å±æ€§ï¼š

```F#
let impl = rle_allChars
let prop = propAdjacentCharactersAreNotSame impl
FsCheck.Check.Quick prop
```

è¿˜æœ‰â€¦

`å¥½çš„ï¼Œé€šè¿‡äº†100æ¬¡æµ‹è¯•ã€‚`

è¿™æ˜¯å‡ºä¹æ„æ–™çš„ã€‚ä¹Ÿè®¸æˆ‘ä»¬åªæ˜¯è¿æ°”ä¸å¥½ï¼Ÿè®©æˆ‘ä»¬å°†é»˜è®¤é…ç½®æ›´æ”¹ä¸º 10000 æ¬¡è¿è¡Œï¼Œè€Œä¸æ˜¯ 100 æ¬¡ã€‚

```F#
let impl = rle_allChars
let prop = propAdjacentCharactersAreNotSame impl
let config = {FsCheck.Config.Default with MaxTest = 10000}
FsCheck.Check.One(config,prop)
```

è¿˜æœ‰â€¦

`å¥½çš„ï¼Œé€šè¿‡äº†10000æ¬¡æµ‹è¯•ã€‚`

â€¦å®ƒä»ç„¶ä¼šè¿‡å»ã€‚è¿™å¯ä¸å¥½ã€‚

å—¯ï¼Œè®©æˆ‘ä»¬æ·»åŠ ä¸€ä¸ªå¿«é€Ÿ `printf` æ¥æ‰“å° FsCheck ç”Ÿæˆçš„å­—ç¬¦ä¸²ï¼Œè¿™æ ·æˆ‘ä»¬å°±å¯ä»¥çœ‹åˆ°å‘ç”Ÿäº†ä»€ä¹ˆã€‚

```F#
let propAdjacentCharactersAreNotSame (impl:RleImpl) inputStr =
  let output = impl inputStr
  printfn "%s" inputStr
  // etc
```

ä»¥ä¸‹æ˜¯ FsCheck ç”Ÿæˆçš„è¾“å…¥å­—ç¬¦ä¸²çš„æ ·å­ï¼š

```
v$D
%q6,NDUwm9~ 8I?a-ruc(@6Gi_+pT;1SdZ|H
E`Vxc(1daN
t/vLH$".5m8RjMrlCUb1J1'
Y[Q?zh^#ELn:0u
```

æˆ‘ä»¬å¯ä»¥çœ‹åˆ°ï¼Œå­—ç¬¦ä¸²æ˜¯éå¸¸éšæœºçš„ï¼Œå‡ ä¹ä»æ¥æ²¡æœ‰ä¸€ç³»åˆ—é‡å¤çš„å­—ç¬¦ã€‚ä»æµ‹è¯• RLE ç®—æ³•çš„è§’åº¦æ¥çœ‹ï¼Œè¿™äº›è¾“å…¥æ˜¯å®Œå…¨æ— ç”¨çš„ï¼

> è¿™ä¸ªæ•…äº‹çš„å¯“æ„æ˜¯ï¼Œå°±åƒå¸¸è§„ TDD ä¸€æ ·ï¼Œç¡®ä¿ä½ ä»å¤±è´¥çš„æµ‹è¯•å¼€å§‹ã€‚åªæœ‰è¿™æ ·ï¼Œä½ æ‰èƒ½ç¡®ä¿ä½ çš„æ­£ç¡®å®ç°æ˜¯å‡ºäºæ­£ç¡®çš„åŸå› ã€‚

å› æ­¤ï¼Œæˆ‘ä»¬ç°åœ¨éœ€è¦åšçš„æ˜¯ç”Ÿæˆ*æœ‰è¶£çš„*è¾“å…¥ï¼Œè€Œä¸æ˜¯éšæœºå­—ç¬¦ä¸²ã€‚

æˆ‘ä»¬å¦‚ä½•åšåˆ°è¿™ä¸€ç‚¹ï¼Ÿé‚£ä¹ˆï¼Œæˆ‘ä»¬å¦‚ä½•åœ¨ä¸è¿›è¡Œç²—ç•¥ `print` è°ƒè¯•çš„æƒ…å†µä¸‹ç›‘æ§è¾“å…¥å†…å®¹å‘¢ï¼Ÿ

è¿™å°†æ˜¯[ä¸‹ä¸€æœŸ](https://fsharpforfunandprofit.com/posts/return-of-the-edfh-2)çš„ä¸»é¢˜ï¼

> æœ¬æ–‡ä¸­ä½¿ç”¨çš„æºä»£ç å¯ä»¥åœ¨[è¿™é‡Œæ‰¾åˆ°](https://github.com/swlaschin/fsharpforfunandprofit.com_code/tree/master/posts/return-of-the-edfh)ã€‚



# 2 ä¸ºåŸºäºå±æ€§çš„æµ‹è¯•ç”Ÿæˆæœ‰è¶£çš„è¾“å…¥

*Part of the "The Return of the EDFH" series (*[link](https://fsharpforfunandprofit.com/posts/return-of-the-edfh-2/#series-toc)*)*

ä»¥åŠå¦‚ä½•å¯¹å®ƒä»¬è¿›è¡Œåˆ†ç±»
2021å¹´2æœˆ15æ—¥

https://fsharpforfunandprofit.com/posts/return-of-the-edfh-2/

åœ¨[ä¸Šä¸€ç¯‡æ–‡ç« ](https://fsharpforfunandprofit.com/posts/return-of-the-edfh)ä¸­ï¼Œæˆ‘ä»¬è¯•å›¾ä¸ºè¡Œç¨‹ç¼–ç ï¼ˆRLEï¼‰å®ç°å®šä¹‰ä¸€äº›å±æ€§ï¼Œä½†ç”±äº FsCheck ç”Ÿæˆçš„éšæœºå€¼ä¸æ˜¯å¾ˆæœ‰ç”¨è€Œé™·å…¥å›°å¢ƒã€‚

åœ¨è¿™ç¯‡æ–‡ç« ä¸­ï¼Œæˆ‘ä»¬å°†æ¢è®¨ç”Ÿæˆâ€œæœ‰è¶£â€è¾“å…¥çš„å‡ ç§æ–¹æ³•ï¼Œä»¥åŠå¦‚ä½•è§‚å¯Ÿå®ƒä»¬ï¼Œä»¥ä¾¿æˆ‘ä»¬å¯ä»¥ç¡®ä¿å®ƒä»¬ç¡®å®æœ‰è¶£ã€‚

## è§‚å¯Ÿç”Ÿæˆçš„æ•°æ®

æˆ‘ä»¬åº”è¯¥åšçš„ç¬¬ä¸€ä»¶äº‹æ˜¯æ·»åŠ æŸç§ç›‘æ§ï¼Œçœ‹çœ‹æœ‰å¤šå°‘è¾“å…¥æ˜¯æœ‰è¶£çš„ã€‚

é‚£ä¹ˆï¼Œä»€ä¹ˆæ˜¯â€œæœ‰è¶£â€çš„è¾“å…¥å‘¢ï¼Ÿå¯¹äºè¿™ç§æƒ…å†µï¼Œå®ƒæ˜¯ä¸€ä¸ªæœ‰ä¸€äº›æ¸¸ç¨‹çš„å­—ç¬¦ä¸²ã€‚è¿™æ„å‘³ç€ä¸€ä¸ªç”±åƒè¿™æ ·çš„éšæœºå­—ç¬¦ç»„æˆçš„å­—ç¬¦ä¸²â€¦

`%q6ï¼ŒNDUwm9~ 8I?a-ruc(@6Gi_+pT;1SdZ|H`

â€¦ä½œä¸º RLE å®ç°çš„è¾“å…¥ä¸æ˜¯å¾ˆæœ‰è¶£ã€‚

åœ¨ä¸å°è¯•é‡æ–°å®ç° RLE é€»è¾‘çš„æƒ…å†µä¸‹ï¼Œç¡®å®šæ˜¯å¦æœ‰æ¸¸ç¨‹çš„ä¸€ç§æ–¹æ³•æ˜¯æŸ¥çœ‹ä¸åŒå­—ç¬¦çš„æ•°é‡æ˜¯å¦è¿œå°äºå­—ç¬¦ä¸²çš„é•¿åº¦ã€‚å¦‚æœè¿™æ˜¯çœŸçš„ï¼Œé‚£ä¹ˆæ ¹æ®[é¸½å­æ´åŸç†](https://en.wikipedia.org/wiki/Pigeonhole_principle)ï¼Œä¸€å®šæœ‰ä¸€äº›å­—ç¬¦çš„é‡å¤ã€‚è¿™å¹¶ä¸èƒ½ç¡®ä¿æœ‰è¿è¡Œï¼Œä½†å¦‚æœæˆ‘ä»¬æŠŠå·®å¼‚åšå¾—è¶³å¤Ÿå¤§ï¼Œå¤§å¤šæ•°â€œæœ‰è¶£â€çš„è¾“å…¥éƒ½ä¼šæœ‰è¿è¡Œã€‚

ä»¥ä¸‹æ˜¯ `isInterestingString` å‡½æ•°çš„å®šä¹‰ï¼š

```F#
let isInterestingString inputStr =
  if System.String.IsNullOrEmpty inputStr then
    false
  else
    let distinctChars =
      inputStr
      |> Seq.countBy id
      |> Seq.length
    distinctChars <= (inputStr.Length / 2)
```

å¦‚æœæˆ‘ä»¬æµ‹è¯•å®ƒï¼Œæˆ‘ä»¬å¯ä»¥çœ‹åˆ°å®ƒå·¥ä½œå¾—å¾ˆå¥½ã€‚

```F#
isInterestingString ""        //=> false
isInterestingString "aa"      //=> true
isInterestingString "abc"     //=> false
isInterestingString "aabbccc" //=> true
isInterestingString "aabaaac" //=> true
isInterestingString "abcabc"  //=> true (but no runs)
```

ä¸ºäº†ç›‘è§†è¾“å…¥æ˜¯å¦æœ‰è¶£ï¼Œæˆ‘ä»¬å°†ä½¿ç”¨ FsCheck å‡½æ•° `Prop.classify`ã€‚

> `Prop.classify` åªæ˜¯å¤„ç†å±æ€§çš„ä¼—å¤šå‡½æ•°ä¹‹ä¸€ã€‚æœ‰å…³å±æ€§çš„æ›´å¤šä¿¡æ¯ï¼Œ[è¯·å‚é˜… FsCheck æ–‡æ¡£](https://fscheck.github.io/FsCheck//Properties.html)ã€‚æˆ–è€…æŸ¥çœ‹[å®Œæ•´çš„ API](https://fscheck.github.io/FsCheck/reference/fscheck-prop.html)ã€‚

ä¸ºäº†æµ‹è¯•æ‰€æœ‰è¿™äº›ï¼Œè®©æˆ‘ä»¬åˆ›å»ºä¸€ä¸ªè™šæ‹Ÿå±æ€§ `propIsInterestingString`ï¼Œæˆ‘ä»¬å¯ä»¥ä½¿ç”¨å®ƒæ¥ç›‘è§† FsCheck ç”Ÿæˆçš„è¾“å…¥ã€‚å®é™…çš„å±æ€§æµ‹è¯•æœ¬èº«åº”è¯¥æ€»æ˜¯æˆåŠŸçš„ï¼Œæ‰€ä»¥æˆ‘ä»¬åªä½¿ç”¨ `true`ã€‚ä»£ç å¦‚ä¸‹ï¼š

```F#
let propIsInterestingString input =
  let isInterestingInput = isInterestingString input

  true // we don't care about the actual test
  |> Prop.classify (not isInterestingInput) "not interesting"
  |> Prop.classify isInterestingInput "interesting"
```

ç°åœ¨è®©æˆ‘ä»¬æ£€æŸ¥ä¸€ä¸‹ï¼š

```F#
FsCheck.Check.Quick propIsInterestingString
// Ok, passed 100 tests (100% not interesting).
```

æˆ‘ä»¬å‘ç° 100% çš„è¾“å…¥éƒ½ä¸æœ‰è¶£ã€‚æ‰€ä»¥æˆ‘ä»¬éœ€è¦æ›´å¥½çš„æŠ•å…¥ï¼

## ç”Ÿæˆæœ‰è¶£çš„å­—ç¬¦ä¸²ï¼Œç¬¬ 1 éƒ¨åˆ†

ä¸€ç§æ–¹æ³•æ˜¯ä½¿ç”¨è¿‡æ»¤å™¨åˆ é™¤æ‰€æœ‰ä¸æ„Ÿå…´è¶£çš„å­—ç¬¦ä¸²ã€‚ä½†è¿™å°†æ˜¯éå¸¸ä½æ•ˆçš„ï¼Œå› ä¸ºæœ‰è¶£çš„å­—ç¬¦ä¸²éå¸¸ç½•è§ã€‚

ç›¸åï¼Œè®©æˆ‘ä»¬ç”Ÿæˆæœ‰è¶£çš„å­—ç¬¦ä¸²ã€‚å¯¹äºæˆ‘ä»¬çš„ç¬¬ä¸€æ¬¡å°è¯•ï¼Œæˆ‘ä»¬å°†ä»éå¸¸ç®€å•çš„äº‹æƒ…å¼€å§‹ï¼šæˆ‘ä»¬å°†ç”Ÿæˆä¸€ä¸ª `'a'` å­—ç¬¦åˆ—è¡¨å’Œä¸€ä¸ª `'b'` å­—ç¬¦åˆ—è¡¨ï¼Œç„¶åå°†è¿™ä¸¤ä¸ªåˆ—è¡¨è¿æ¥èµ·æ¥ï¼Œç»™æˆ‘ä»¬ä¸€äº›ä¸é”™çš„è¿è¡Œã€‚

ä¸ºäº†åšåˆ°è¿™ä¸€ç‚¹ï¼Œæˆ‘ä»¬å°†æ„å»ºè‡ªå·±çš„ç”Ÿæˆå™¨ï¼ˆè§å‰é¢å¯¹ç”Ÿæˆå™¨å’Œæ”¶ç¼©å™¨çš„è®¨è®ºï¼‰ã€‚FsCheck æä¾›äº†ä¸€ç»„æœ‰ç”¨çš„å‡½æ•°æ¥åˆ¶ä½œç”Ÿæˆå™¨ï¼Œä¾‹å¦‚ `Gen.constant` ç”¨äºç”Ÿæˆå¸¸é‡ï¼Œ`Gen.choose` ç”¨äºä»åŒºé—´ä¸­é€‰å–éšæœºæ•°ï¼Œ`Gen.elements` ç”¨äºä»åˆ—è¡¨ä¸­é€‰å–éšæœºå…ƒç´ ã€‚ä¸€æ—¦ä½ æœ‰äº†ä¸€ä¸ªåŸºæœ¬çš„ç”Ÿæˆå™¨ï¼Œä½ å°±å¯ä»¥ `map` å’Œ `filter` å®ƒçš„è¾“å‡ºï¼Œè¿˜å¯ä»¥ç”¨ `map2`ã€`oneOf` ç­‰ç»„åˆå¤šä¸ªç”Ÿæˆå™¨ã€‚

> æœ‰å…³ä½¿ç”¨ç”Ÿæˆå™¨çš„æ›´å¤šä¿¡æ¯ï¼Œè¯·å‚é˜… FsCheck æ–‡æ¡£ã€‚
>
> - [ç”Ÿæˆå™¨ä½¿ç”¨æ¦‚è¿°](https://fscheck.github.io/FsCheck//TestData.html)
> - [ç”Ÿæˆå™¨ API](https://fscheck.github.io/FsCheck/reference/fscheck-gen.html)

ä¸‹é¢æ˜¯æˆ‘ä»¬ä½¿ç”¨ç”Ÿæˆå™¨çš„ä»£ç ï¼š

```F#
let arbTwoCharString =
  // helper function to create strings from a list of chars
  let listToString chars =
    chars |> List.toArray |> System.String

  // random lists of 'a's and 'b's
  let genListA = Gen.constant 'a' |> Gen.listOf
  let genListB  = Gen.constant 'b' |> Gen.listOf

  (genListA,genListB)
  ||> Gen.map2 (fun listA listB -> listA @ listB )
  |> Gen.map listToString
  |> Arb.fromGen
```

æˆ‘ä»¬ç”Ÿæˆä¸€ä¸ª `'a'` å­—ç¬¦åˆ—è¡¨å’Œä¸€ä¸ª `'b'` å­—ç¬¦åˆ—è¡¨ï¼Œç„¶åä½¿ç”¨ `map2` å°†å®ƒä»¬è¿æ¥èµ·æ¥ï¼Œç„¶åå°†ç»“æœåˆ—è¡¨è½¬æ¢ä¸ºå­—ç¬¦ä¸²ã€‚ä½œä¸ºæœ€åä¸€æ­¥ï¼Œæˆ‘ä»¬ä»ç”Ÿæˆå™¨ä¸­æ„å»ºä¸€ä¸ª `Arbitrary`ï¼Œè¿™æ˜¯æˆ‘ä»¬åœ¨æµ‹è¯•é˜¶æ®µæ‰€éœ€çš„ã€‚æˆ‘ä»¬ç°åœ¨ä¸æä¾›å®šåˆ¶æ”¶ç¼©å™¨ã€‚

æ¥ä¸‹æ¥ï¼Œè®©æˆ‘ä»¬ä»æ–°ç”Ÿæˆå™¨ä¸­é‡‡æ ·ä¸€äº›éšæœºå­—ç¬¦ä¸²ï¼Œçœ‹çœ‹å®ƒä»¬æ˜¯ä»€ä¹ˆæ ·å­çš„ï¼š

```F#
arbTwoCharString.Generator |> Gen.sample 10 10
(*
[ "aaabbbbbbb"; "aaaaaaaaabb"; "b"; "abbbbbbbbbb";
  "aaabbbb"; "bbbbbb"; "aaaaaaaabbbbbbb";
  "a"; "aabbbb"; "aaaaabbbbbbbbb"]
*)
```

çœ‹èµ·æ¥ä¸é”™ã€‚å¤§å¤šæ•°å­—ç¬¦ä¸²éƒ½æœ‰è¿è¡Œï¼Œæ­£å¦‚æˆ‘ä»¬æ‰€å¸Œæœ›çš„é‚£æ ·ã€‚

ç°åœ¨ï¼Œæˆ‘ä»¬å¯ä»¥å°†æ­¤ç”Ÿæˆå™¨åº”ç”¨äºæˆ‘ä»¬ä¹‹å‰åˆ›å»ºçš„ `propIsInterestingString` å±æ€§ã€‚æˆ‘ä»¬å°†ä½¿ç”¨ `Prop.forAll` ä½¿ç”¨è‡ªå®šä¹‰ç”Ÿæˆå™¨æ„é€ æ–°å±æ€§ï¼Œç„¶åä½¿ç”¨ `Check.Quick` ä»¥é€šå¸¸çš„æ–¹å¼æµ‹è¯•æ–°å±æ€§ã€‚

```F#
// make a new property from the old one, with input from our generator
let prop = Prop.forAll arbTwoCharString propIsInterestingString
// check it
Check.Quick prop

(*
Ok, passed 100 tests.
97% interesting.
3% not interesting.
*)
```

è€Œä¸”è¿™ä¸ªè¾“å‡ºè¦å¥½å¾—å¤šï¼å‡ ä¹æ‰€æœ‰çš„è¾“å…¥éƒ½å¾ˆæœ‰è¶£ã€‚

## ç”Ÿæˆæœ‰è¶£çš„å­—ç¬¦ä¸²ï¼Œç¬¬ 2 éƒ¨åˆ†

æˆ‘ä»¬ç”Ÿæˆçš„å­—ç¬¦ä¸²æœ€å¤šæœ‰ä¸¤ä¸ªæ¸¸ç¨‹ï¼Œè¿™å¹¶ä¸èƒ½å¾ˆå¥½åœ°ä»£è¡¨æˆ‘ä»¬æƒ³è¦æ¸¸ç¨‹é•¿åº¦ç¼–ç ï¼ˆrun-length encodeï¼‰çš„çœŸå®å­—ç¬¦ä¸²ã€‚æˆ‘ä»¬å¯ä»¥å¢å¼ºç”Ÿæˆå™¨ä»¥åŒ…å«å¤šä¸ªå­—ç¬¦åˆ—è¡¨ï¼Œä½†å®ƒæœ‰ç‚¹å¤æ‚ï¼Œæ‰€ä»¥è®©æˆ‘ä»¬ä»å®Œå…¨ä¸åŒçš„æ–¹å‘æ¥å¤„ç†è¿™ä¸ªé—®é¢˜ã€‚

è¡Œç¨‹ç¼–ç ï¼ˆrun-length encodingï¼‰æœ€å¸¸è§çš„ç”¨é€”ä¹‹ä¸€æ˜¯å‹ç¼©å›¾åƒã€‚æˆ‘ä»¬å¯ä»¥å°†å•è‰²å›¾åƒè§†ä¸º 0 å’Œ 1 çš„æ•°ç»„ï¼Œå…¶ä¸­ 1 è¡¨ç¤ºé»‘è‰²åƒç´ ã€‚ç°åœ¨è®©æˆ‘ä»¬è€ƒè™‘ä¸€ä¸ªåªæœ‰å‡ ä¸ªé»‘è‰²åƒç´ çš„å›¾åƒï¼Œè¿™åè¿‡æ¥æ„å‘³ç€å¤§é‡çš„ç™½è‰²åƒç´ ï¼Œéå¸¸é€‚åˆä½œä¸ºæˆ‘ä»¬æµ‹è¯•çš„è¾“å…¥ã€‚

æˆ‘ä»¬å¦‚ä½•ç”Ÿæˆè¿™æ ·çš„â€œå›¾åƒâ€ï¼Ÿä»ä¸€ç»„ç™½è‰²åƒç´ å¼€å§‹ï¼Œéšæœºå°†å…¶ä¸­ä¸€äº›ç¿»è½¬ä¸ºé»‘è‰²ï¼Œæ€ä¹ˆæ ·ï¼Ÿ

é¦–å…ˆï¼Œæˆ‘ä»¬éœ€è¦ä¸€ä¸ªè¾…åŠ©å‡½æ•°æ¥éšæœºç¿»è½¬å­—ç¬¦ä¸²ä¸­çš„â€œä½â€ï¼š

```F#
let flipRandomBits (str:string) = gen {

  // convert input to a mutable array
  let arr = str |> Seq.toArray

  // get a random subset of pixels
  let max = str.Length - 1
  let! indices = Gen.subListOf [0..max]

  // flip them
  for i in indices do arr.[i] <- '1'

  // convert back to a string
  return (System.String arr)
  }
```

ç„¶åæˆ‘ä»¬å¯ä»¥æ„é€ ä¸€ä¸ªç”Ÿæˆå™¨ï¼š

```F#
let arbPixels =
  gen {
    // randomly choose a length up to 50,
    // and set all pixels to 0
    let! pixelCount = Gen.choose(1,50)
    let image1 = String.replicate pixelCount "0"

    // then flip some pixels
    let! image2 = flipRandomBits image1

    return image2
    }
  |> Arb.fromGen // create a new Arb from the generator
```

ç°åœ¨è®©æˆ‘ä»¬å¯¹ç”Ÿæˆå™¨è¿›è¡Œé‡‡æ ·ï¼š

```F#
arbPixels.Generator |> Gen.sample 10 10
(*
"0001001000000000010010010000000";
"00000000000000000000000000000000000000000000100";
"0001111011111011110000011111";
"0101101101111111011010";
"10000010001011000001000001000001101000100100100000";
"0000000000001000";
"00010100000101000001010000100100001010000010100";
"00000000000000000000000000000000000000000";
"0000110101001010010";
"11100000001100011000000000000000001"
*)
```

çœ‹èµ·æ¥ä¸é”™â€”â€”åªæœ‰ä¸€ä¸ªå­—ç¬¦ä¸²ä¸­æ²¡æœ‰ runã€‚æˆ‘ä»¬æ€»æ˜¯å¸Œæœ›åœ¨æˆ‘ä»¬çš„ç¤ºä¾‹ä¸­æœ‰ä¸€äº›ç©ºå­—ç¬¦ä¸²å’Œæ²¡æœ‰ run çš„å­—ç¬¦ä¸²ï¼Œä»¥æ£€æŸ¥è¾¹ç¼˜æƒ…å†µã€‚

æˆ‘ä»¬ç°åœ¨å¯ä»¥ä½¿ç”¨è¿™ä¸ªæ–°ç”Ÿæˆå™¨å°è¯• `propIsInterestingString` å±æ€§ã€‚

```F#
// make a new property from the old one, with input from our generator
let prop = Prop.forAll arbPixels propIsInterestingString
// check it
Check.Quick prop

(*
Ok, passed 100 tests.
94% interesting.
6% not interesting.
*)
```

åŒæ ·ï¼Œæˆ‘ä»¬å¾—åˆ°äº† 94% æœ‰è¶£å­—ç¬¦ä¸²çš„æœ‰ç”¨ç»“æœã€‚

## æ˜¯æ—¶å€™æµ‹è¯• EDFH çš„æ€§èƒ½äº†

ç°åœ¨æˆ‘ä»¬æœ‰äº†ä¸€ç§å¯é çš„ç”Ÿæˆå­—ç¬¦ä¸²çš„æ–¹æ³•ï¼Œæˆ‘ä»¬å¯ä»¥é‡æ–°è®¿é—®ä¸Šä¸€ç¯‡æ–‡ç« ä¸­çš„å±æ€§ï¼Œçœ‹çœ‹ EDFH çš„å®ç°æ˜¯å¦é€šè¿‡ã€‚

ä½œä¸ºæé†’ï¼Œä¸ºäº†æ­£ç¡®å®ç° RLEï¼Œä»¥ä¸‹æ˜¯æˆ‘ä»¬æå‡ºçš„å±æ€§ï¼š

- è¾“å‡ºå¿…é¡»åŒ…å«è¾“å…¥ä¸­çš„æ‰€æœ‰å­—ç¬¦
- è¾“å‡ºä¸­çš„ä¸¤ä¸ªç›¸é‚»å­—ç¬¦ä¸èƒ½ç›¸åŒ
- è¾“å‡ºä¸­çš„è¡Œç¨‹é•¿åº¦ä¹‹å’Œå¿…é¡»ç­‰äºè¾“å…¥çš„æ€»é•¿åº¦
- å¦‚æœè¾“å…¥è¢«åè½¬ï¼Œè¾“å‡ºä¹Ÿå¿…é¡»è¢«åè½¬

ä»¥ä¸‹æ˜¯æ¯ä¸ªçš„ä»£ç ï¼š

### Prop #1ï¼šè¾“å‡ºå¿…é¡»åŒ…å«è¾“å…¥ä¸­çš„æ‰€æœ‰å­—ç¬¦

```F#
// A RLE implementation has this signature
type RleImpl = string -> (char*int) list
let propUsesAllCharacters (impl:RleImpl) inputStr =
  let output = impl inputStr
  let expected =
    if System.String.IsNullOrEmpty inputStr then
      []
    else
      inputStr
      |> Seq.distinct
      |> Seq.toList
  let actual =
    output
    |> Seq.map fst
    |> Seq.distinct
    |> Seq.toList
  expected = actual
```

æ³¨æ„ï¼šå®ç°æ—¶ï¼Œæ­¤å±æ€§å®é™…ä¸Šæ¯”â€œåŒ…å«è¾“å…¥ä¸­çš„æ‰€æœ‰å­—ç¬¦â€*æ›´å¼º*ã€‚å¦‚æœæˆ‘ä»¬æƒ³è¦è¿™æ ·åšï¼Œæˆ‘ä»¬åº”è¯¥åœ¨æ¯”è¾ƒä¹‹å‰å°† `expected` å’Œ `actual` è½¬æ¢ä¸ºæ— åºé›†ã€‚ä½†æ˜¯ï¼Œç”±äºæˆ‘ä»¬å°†å®ƒä»¬ä½œä¸ºåˆ—è¡¨ä¿ç•™ï¼Œå› æ­¤å®ç°çš„å±æ€§å®é™…ä¸Šâ€œåŒ…å«è¾“å…¥ä¸­çš„æ‰€æœ‰å­—ç¬¦ï¼Œ*å¹¶ä¸”é¡ºåºç›¸åŒ*â€ã€‚

### Prop #2ï¼šè¾“å‡ºä¸­çš„ä¸¤ä¸ªç›¸é‚»å­—ç¬¦ä¸èƒ½ç›¸åŒ

```F#
let propAdjacentCharactersAreNotSame (impl:RleImpl) inputStr =
  let output = impl inputStr
  let actual =
    output
    |> Seq.map fst
    |> Seq.toList
  let expected =
    actual
    |> removeDupAdjacentChars // should have no effect
  expected = actual // should be the same
```

æé†’ï¼šæ­¤ä»£ç ä¸­çš„ `removeDupAdjacentChars` å‡½æ•°æ˜¯åœ¨[ä¸Šä¸€ç¯‡æ–‡ç« ](https://fsharpforfunandprofit.com/posts/return-of-the-edfh/#the-adjacent-characters-are-not-the-same-property)ä¸­å®šä¹‰çš„ã€‚

### Prop #3ï¼šè¾“å‡ºä¸­çš„æ¸¸ç¨‹é•¿åº¦ä¹‹å’Œå¿…é¡»ç­‰äºè¾“å…¥çš„é•¿åº¦

```F#
let propRunLengthSum_eq_inputLength (impl:RleImpl) inputStr =
  let output = impl inputStr
  let expected = inputStr.Length
  let actual = output |> List.sumBy snd
  expected = actual // should be the same
```

åœ¨è¿™é‡Œï¼Œæˆ‘ä»¬åªæ˜¯å¯¹æ¯ä¸ª `(char,run-length)` å…ƒç»„çš„ç¬¬äºŒéƒ¨åˆ†æ±‚å’Œã€‚

### Prop #4ï¼šå¦‚æœè¾“å…¥è¢«åè½¬ï¼Œè¾“å‡ºä¹Ÿå¿…é¡»è¢«åè½¬

```F#
/// Helper to reverse strings
let strRev (str:string) =
  str
  |> Seq.rev
  |> Seq.toArray
  |> System.String

let propInputReversed_implies_outputReversed (impl:RleImpl) inputStr =
  // original
  let output1 =
    inputStr |> impl

  // reversed
  let output2 =
    inputStr |> strRev |> impl

  List.rev output1 = output2 // should be the same
```

### ç»„åˆå±æ€§

æœ€åï¼Œæˆ‘ä»¬å¯ä»¥å°†æ‰€æœ‰å››ä¸ªå±æ€§ç»„åˆæˆä¸€ä¸ªå¤åˆå±æ€§ã€‚å››ä¸ªå­å±æ€§ä¸­çš„æ¯ä¸€ä¸ªéƒ½æœ‰ä¸€ä¸ªå¸¦æœ‰ `@|` çš„æ ‡ç­¾ï¼Œè¿™æ ·å½“å¤åˆå±æ€§å¤±è´¥æ—¶ï¼Œæˆ‘ä»¬å°±çŸ¥é“æ˜¯å“ªä¸ªå­å±æ€§å¯¼è‡´äº†å¤±è´¥ã€‚

```F#
let propRle (impl:RleImpl) inputStr =
  let prop1 =
    propUsesAllCharacters impl inputStr
    |@ "propUsesAllCharacters"
  let prop2 =
    propAdjacentCharactersAreNotSame impl inputStr
    |@ "propAdjacentCharactersAreNotSame"
  let prop3 =
    propRunLengthSum_eq_inputLength impl inputStr
    |@ "propRunLengthSum_eq_inputLength"
  let prop4 =
    propInputReversed_implies_outputReversed impl inputStr
    |@ "propInputReversed_implies_outputReversed"

  // combine them
  prop1 .&. prop2 .&. prop3 .&. prop4
```

## æµ‹è¯• EDFH å®ç°

ç°åœ¨ï¼Œæœ€åï¼Œæˆ‘ä»¬å¯ä»¥æ ¹æ®å¤åˆå±æ€§æµ‹è¯• EDFH å®ç°ã€‚

ç¬¬ä¸€ä¸ª EDFH å®ç°åªæ˜¯è¿”å›äº†ä¸€ä¸ªç©ºåˆ—è¡¨ã€‚

```F#
/// Return an empty list
let rle_empty (inputStr:string) : (char*int) list =
  []
```

æˆ‘ä»¬å¸Œæœ›å®ƒåœ¨ç¬¬ä¸€ä¸ªå±æ€§ä¸Šå¤±è´¥ï¼šâ€œè¾“å‡ºå¿…é¡»åŒ…å«è¾“å…¥ä¸­çš„æ‰€æœ‰å­—ç¬¦â€ã€‚

```F#
let prop = Prop.forAll arbPixels (propRle rle_empty)
// -- expect to fail on propUsesAllCharacters

// check it
Check.Quick prop
(*
Falsifiable, after 1 test (0 shrinks)
Label of failing property: propUsesAllCharacters
*)
```

ç¡®å®å¦‚æ­¤ã€‚

### EDFH å®ç° #2

ä¸‹ä¸€ä¸ª EDFH å®ç°åªæ˜¯å°†æ¯ä¸ª char ä½œä¸ºè‡ªå·±çš„ run è¿”å›ï¼Œrun é•¿åº¦ä¸º 1ã€‚

```F#
/// Return each char with count 1
let rle_allChars inputStr =
  // add null check
  if System.String.IsNullOrEmpty inputStr then
    []
  else
    inputStr
    |> Seq.toList
    |> List.map (fun ch -> (ch,1))
```

æˆ‘ä»¬å¸Œæœ›å®ƒåœ¨ç¬¬äºŒä¸ªå±æ€§ä¸Šå¤±è´¥ï¼šâ€œè¾“å‡ºä¸­æ²¡æœ‰ä¸¤ä¸ªç›¸é‚»çš„å­—ç¬¦å¯ä»¥ç›¸åŒâ€ã€‚

```F#
let prop = Prop.forAll arbPixels (propRle rle_allChars)
// -- expect to fail on propAdjacentCharactersAreNotSame

// check it
Check.Quick prop
(*
Falsifiable, after 1 test (0 shrinks)
Label of failing property: propAdjacentCharactersAreNotSame
*)
```

ç¡®å®å¦‚æ­¤ã€‚

### EDFH å®ç° #3

ç¬¬ä¸‰ä¸ªEDFHå®ç°é€šè¿‡æ‰§è¡Œä¸åŒçš„ç¬¬ä¸€ä¸ªæ¥é¿å…é‡å¤å­—ç¬¦é—®é¢˜ã€‚

```F#
let rle_distinct inputStr =
  // add null check
  if System.String.IsNullOrEmpty inputStr then
    []
  else
    inputStr
    |> Seq.distinct
    |> Seq.toList
    |> List.map (fun ch -> (ch,1))
```

å®ƒå°†ä¼ é€’ç¬¬äºŒä¸ªå±æ€§ï¼šâ€œè¾“å‡ºä¸­æ²¡æœ‰ä¸¤ä¸ªç›¸é‚»çš„å­—ç¬¦å¯ä»¥ç›¸åŒâ€ï¼Œä½†æˆ‘ä»¬é¢„è®¡å®ƒåœ¨ç¬¬ä¸‰ä¸ªå±æ€§ä¸Šä¼šå¤±è´¥ï¼šâ€œè¾“å‡ºçš„æ¸¸ç¨‹é•¿åº¦ä¹‹å’Œå¿…é¡»ç­‰äºè¾“å…¥çš„æ€»é•¿åº¦â€ã€‚

```F#
let prop = Prop.forAll arbPixels (propRle rle_distinct)
// -- expect to fail on propRunLengthSum_eq_inputLength

// check it
Check.Quick prop
(*
Falsifiable, after 1 test (0 shrinks)
Label of failing property: propRunLengthSum_eq_inputLength
*)
```

ç¡®å®å¦‚æ­¤ï¼

### EDFH å®ç° #4

æœ€åä¸€ä¸ª EDFH å®ç°é¿å…äº†é‡å¤å­—ç¬¦çš„é—®é¢˜ï¼Œå¹¶é€šè¿‡æ‰§è¡Œ `groupBy` æ“ä½œè·å¾—äº†æ­£ç¡®çš„æ•´ä½“æ¸¸ç¨‹é•¿åº¦ã€‚

```F#
let rle_countBy inputStr =
  if System.String.IsNullOrEmpty inputStr then
    []
  else
    inputStr
    |> Seq.countBy id
    |> Seq.toList
```

è¿™å°±æ˜¯ä¸ºä»€ä¹ˆæˆ‘ä»¬æ·»åŠ äº†ç¬¬å››ä¸ªå±æ€§æ¥æ•æ‰è¿™ä¸€ç‚¹ï¼šâ€œå¦‚æœè¾“å…¥è¢«åè½¬ï¼Œè¾“å‡ºä¹Ÿå¿…é¡»è¢«åè½¬â€ã€‚

```F#
let prop = Prop.forAll arbPixels (propRle rle_countBy)
// -- expect to fail on propInputReversed_implies_outputReversed

// check it
Check.Quick prop
(*
Falsifiable, after 1 test (0 shrinks)
Label of failing property: propInputReversed_implies_outputReversed
*)
```

å®ƒå¦‚é¢„æœŸèˆ¬å¤±è´¥äº†ã€‚

## æµ‹è¯•æ­£ç¡®çš„å®ç°

åœ¨æ‰€æœ‰è¿™äº›ç³Ÿç³•çš„å®ç°ä¹‹åï¼Œè®©æˆ‘ä»¬çœ‹çœ‹ä¸€äº›æ­£ç¡®çš„å®ç°ã€‚æˆ‘ä»¬å¯ä»¥ä½¿ç”¨æˆ‘ä»¬çš„å››ä¸ªå±æ€§æ¥ç¡®ä¿¡ç‰¹å®šçš„å®ç°æ˜¯æ­£ç¡®çš„ã€‚

### æ­£ç¡®å®ç° #1

æˆ‘ä»¬çš„ç¬¬ä¸€ä¸ªå®ç°å°†ä½¿ç”¨é€’å½’ã€‚å®ƒå°†å»æ‰ç¬¬ä¸€ä¸ªå­—ç¬¦ï¼Œç•™ä¸‹ä¸€ä¸ªè¾ƒå°çš„åˆ—è¡¨ã€‚ç„¶åï¼Œå®ƒå°†å¯¹è¾ƒå°çš„åˆ—è¡¨åº”ç”¨ç›¸åŒçš„é€»è¾‘ã€‚

```F#
let rle_recursive inputStr =

  // inner recursive function
  let rec loop input =
    match input with
    | [] -> []
    | head::_ ->
      [
      // get a run
      let runLength = List.length (List.takeWhile ((=) head) input)
      // return it
      yield head,runLength
      // skip the run and repeat
      yield! loop (List.skip runLength input)
      ]

  // main
  inputStr |> Seq.toList |> loop
```

å¦‚æœæˆ‘ä»¬æµ‹è¯•ä¸€ä¸‹ï¼Œå®ƒä¼¼ä¹æŒ‰é¢„æœŸå·¥ä½œã€‚

```F#
rle_recursive "aaaabbbcca"
// [('a', 4); ('b', 3); ('c', 2); ('a', 1)]
```

ä½†çœŸçš„å—ï¼Ÿè®©æˆ‘ä»¬é€šè¿‡å±æ€§æ£€æŸ¥å™¨æ¥ç¡®è®¤ï¼š

```F#
let prop = Prop.forAll arbPixels (propRle rle_recursive)
// -- expect it to not fail

// check it
Check.Quick prop
(*
Ok, passed 100 tests.
*)
```

æ˜¯çš„ï¼Œæ²¡æœ‰å±æ€§å¤±è´¥ï¼

### æ­£ç¡®å®ç° #2

ä¸Šé¢çš„é€’å½’å®ç°å¯èƒ½å¯¹éå¸¸å¤§çš„è¾“å…¥å­—ç¬¦ä¸²æœ‰ä¸€äº›é—®é¢˜ã€‚é¦–å…ˆï¼Œå†…éƒ¨å¾ªç¯ä¸æ˜¯å°¾éƒ¨é€’å½’çš„ï¼Œå› æ­¤å †æ ˆå¯èƒ½ä¼šæº¢å‡ºã€‚æ­¤å¤–ï¼Œé€šè¿‡ä¸æ–­åˆ›å»ºå­åˆ—è¡¨ï¼Œå®ƒä¼šäº§ç”Ÿå¤§é‡åƒåœ¾ï¼Œä»è€Œå½±å“æ€§èƒ½ã€‚

å¦ä¸€ç§æ–¹æ³•æ˜¯ä½¿ç”¨ `Seq.fold` è¿­ä»£è¾“å…¥ä¸€æ¬¡ã€‚ä»¥ä¸‹æ˜¯ä¸€ä¸ªåŸºæœ¬å®ç°ï¼š

```F#
let rle_fold inputStr =
  // This implementation iterates over the list
  // using the 'folder' function and accumulates
  // into 'acc'

  // helper
  let folder (currChar,currCount,acc) inputChar =
    if currChar <> inputChar then
      // push old run onto accumulator
      let acc' = (currChar,currCount) :: acc
      // start new run
      (inputChar,1,acc')
    else
      // same run, so increment count
      (currChar,currCount+1,acc)

  // helper
  let toFinalList (currChar,currCount,acc) =
    // push final run onto acc
    (currChar,currCount) :: acc
    |> List.rev

  // main
  if System.String.IsNullOrEmpty inputStr then
    []
  else
    let head = inputStr.[0]
    let tail = inputStr.[1..inputStr.Length-1]
    let initialState = (head,1,[])
    tail
    |> Seq.fold folder initialState
    |> toFinalList
```

æˆ‘ä»¬å¯ä»¥é€šè¿‡ä½¿ç”¨å¯å˜ç´¯åŠ å™¨ã€ä½¿ç”¨æ•°ç»„è€Œä¸æ˜¯åˆ—è¡¨ç­‰æ¥è¿›ä¸€æ­¥ä¼˜åŒ–è¿™ä¸€ç‚¹ã€‚ä½†è¿™è¶³ä»¥æ¼”ç¤ºåŸç†ã€‚

ä»¥ä¸‹æ˜¯ä¸€äº›äº¤äº’å¼æµ‹è¯•ï¼Œä»¥ç¡®ä¿å…¶æŒ‰é¢„æœŸå·¥ä½œï¼š

```F#
rle_fold ""    //=> []
rle_fold "a"   //=> [('a',1)]
rle_fold "aa"  //=> [('a',2)]
rle_fold "ab"  //=> [('a',1); ('b',1)]
rle_fold "aab" //=> [('a',2); ('b',1)]
rle_fold "abb" //=> [('a',1); ('b',2)]
rle_fold "aaaabbbcca"
  //=> [('a',4); ('b',3); ('c',2); ('a',1)]
```

å½“ç„¶ï¼Œä½¿ç”¨å±æ€§æ£€æŸ¥å™¨æ˜¯ä¸€ç§æ›´å¥½çš„æ–¹æ³•æ¥ç¡®ä¿ï¼š

```F#
let prop = Prop.forAll arbPixels (propRle rle_fold)
// -- expect it to not fail

// check it
Check.Quick prop
(*
Ok, passed 100 tests.
*)
```

å®ƒç¡®å®é€šè¿‡äº†æ‰€æœ‰çš„æµ‹è¯•ã€‚

å› æ­¤ï¼Œé€»è¾‘æ˜¯æ­£ç¡®çš„ï¼Œä½†å¦‚ä¸Šæ‰€è¿°ï¼Œåœ¨è€ƒè™‘ç”Ÿäº§å°±ç»ªä¹‹å‰ï¼Œæˆ‘ä»¬è¿˜åº”è¯¥å¯¹å¤§å‹è¾“å…¥å’Œä¼˜åŒ–è¿›è¡Œä¸€äº›æ€§èƒ½æµ‹è¯•ã€‚é‚£å®Œå…¨æ˜¯å¦ä¸€ä¸ªè¯é¢˜äº†ï¼

ä¼˜åŒ–æœ‰æ—¶ä¼šå¼•å…¥é”™è¯¯ï¼Œä½†ç°åœ¨æˆ‘ä»¬æœ‰äº†è¿™äº›å±æ€§ï¼Œæˆ‘ä»¬å¯ä»¥ä»¥åŒæ ·çš„æ–¹å¼æµ‹è¯•ä¼˜åŒ–åçš„ä»£ç ï¼Œå¹¶ç¡®ä¿¡ä»»ä½•é”™è¯¯éƒ½ä¼šç«‹å³è¢«æ£€æµ‹åˆ°ã€‚

## æ‘˜è¦

åœ¨è¿™ç¯‡æ–‡ç« ä¸­ï¼Œæˆ‘ä»¬é¦–å…ˆæ‰¾åˆ°äº†ä¸€ç§ç”Ÿæˆâ€œæœ‰è¶£â€è¾“å…¥çš„æ–¹æ³•ï¼Œç„¶åä½¿ç”¨è¿™äº›è¾“å…¥ï¼Œå¯¹ EDFH å®ç°è¿è¡Œä¸Šæ¬¡çš„å±æ€§ã€‚ä»–ä»¬éƒ½å¤±è´¥äº†ï¼ç„¶åæˆ‘ä»¬å®šä¹‰äº†ä¸¤ä¸ªæ»¡è¶³æ‰€æœ‰å±æ€§çš„æ­£ç¡®å®ç°ã€‚

é‚£ä¹ˆï¼Œæˆ‘ä»¬ç°åœ¨ç»“æŸäº†å—ï¼Ÿä¸æ˜¯çš„ã€‚äº‹å®è¯æ˜ï¼ŒEDFH ä»ç„¶å¯ä»¥åˆ›å»ºä¸€ä¸ªæ»¡è¶³æ‰€æœ‰å±æ€§çš„å®ç°ï¼ä¸ºäº†æœ€ç»ˆå‡»è´¥EDFHï¼Œæˆ‘ä»¬éœ€è¦åšå¾—æ›´å¥½ã€‚

è¿™å°†æ˜¯[ä¸‹ä¸€æœŸ](https://fsharpforfunandprofit.com/posts/return-of-the-edfh-3)çš„ä¸»é¢˜ã€‚

> æœ¬æ–‡ä¸­ä½¿ç”¨çš„æºä»£ç å¯ä»¥åœ¨[è¿™é‡Œæ‰¾åˆ°](https://github.com/swlaschin/fsharpforfunandprofit.com_code/tree/master/posts/return-of-the-edfh-2)ã€‚

# 3 EDFH å†æ¬¡è¢«å‡»è´¥

*Part of the "The Return of the EDFH" series (*[link](https://fsharpforfunandprofit.com/posts/return-of-the-edfh-3/#series-toc)*)*

2021å¹´2æœˆ16æ—¥

https://fsharpforfunandprofit.com/posts/return-of-the-edfh-3/

åœ¨[æœ¬ç³»åˆ—çš„ç¬¬ä¸€ç¯‡æ–‡ç« ](https://fsharpforfunandprofit.com/posts/return-of-the-edfh)ä¸­ï¼Œæˆ‘ä»¬æå‡ºäº†ä¸€äº›å¯ç”¨äºæµ‹è¯•è¡Œç¨‹ç¼–ç å®ç°çš„å±æ€§ï¼š

- è¾“å‡ºå¿…é¡»ä»¥ç›¸åŒçš„é¡ºåºåŒ…å«è¾“å…¥ä¸­çš„æ‰€æœ‰å­—ç¬¦
- è¾“å‡ºä¸­çš„ä¸¤ä¸ªç›¸é‚»å­—ç¬¦ä¸èƒ½ç›¸åŒ
- è¾“å‡ºä¸­çš„è¡Œç¨‹é•¿åº¦ä¹‹å’Œå¿…é¡»ç­‰äºè¾“å…¥çš„æ€»é•¿åº¦
- å¦‚æœè¾“å…¥è¢«åè½¬ï¼Œè¾“å‡ºä¹Ÿå¿…é¡»è¢«åè½¬

åœ¨[ä¸Šä¸€ç¯‡æ–‡ç« ](https://fsharpforfunandprofit.com/posts/return-of-the-edfh-2)ä¸­ï¼Œæˆ‘ä»¬æµ‹è¯•äº†ç”±[åœ°ç‹±ä¼ä¸šå¼€å‘äººå‘˜](https://fsharpforfunandprofit.com/pbt)åˆ›å»ºçš„å„ç§RLEå®ç°ï¼Œå¹¶å¾ˆé«˜å…´å®ƒä»¬éƒ½å¤±è´¥äº†ã€‚

ä½†æ˜¯ï¼Œè¿™å››ä¸ªå±æ€§è¶³ä»¥æ­£ç¡®æ£€æŸ¥ RLE å®ç°å—ï¼ŸEDFH èƒ½å¦åˆ›å»ºä¸€ä¸ªæ»¡è¶³è¿™äº›å±æ€§ä½†åˆé”™è¯¯çš„å®ç°ï¼Ÿ

ç­”æ¡ˆæ˜¯è‚¯å®šçš„ï¼EDFH å¯ä»¥è·å–æ­£ç¡®å®ç°çš„è¾“å‡ºï¼Œç„¶ååœ¨åˆ—è¡¨çš„å¼€å¤´å’Œç»“å°¾æ·»åŠ ä¸€äº›é¢å¤–çš„å­—ç¬¦æ¥ç ´åç­”æ¡ˆã€‚ç¡®åˆ‡åœ°è¯´ï¼Œè¦æ·»åŠ çš„å†…å®¹å—åˆ°ä¸Šè¿°å±æ€§çš„çº¦æŸï¼š

- â€œä¸¤ä¸ªç›¸é‚»å­—ç¬¦â€å±æ€§æ„å‘³ç€æ–°å‰ç¼€å¿…é¡»ä¸ç¬¬ä¸€ä¸ªå­—ç¬¦ä¸åŒã€‚
- ä½†æ˜¯â€œæ‰€æœ‰å­—ç¬¦é¡ºåºç›¸åŒâ€å±æ€§æ„å‘³ç€ EDFH ä¸èƒ½åªæ·»åŠ ä¸åŒçš„å­—ç¬¦ï¼Œå› ä¸ºè¿™ä¼šç ´åâ€œé¡ºåºç›¸åŒâ€ã€‚è§£å†³æ–¹æ³•æ˜¯ EDFH æ·»åŠ å‰ä¸¤ä¸ªå­—ç¬¦çš„å‰¯æœ¬ï¼
- â€œæ¸¸ç¨‹é•¿åº¦ä¹‹å’Œâ€å±æ€§æ„å‘³ç€æ–°å‰ç¼€çš„æ¸¸ç¨‹é•¿åº¦å¿…é¡»ä»åç»­æ¸¸ç¨‹ä¸­çªƒå–è®¡æ•°ã€‚å¦‚æœæˆ‘ä»¬æ²¡æœ‰çªƒå–è®¡æ•°ï¼Œå¹¶ä½¿ç”¨0ä½œä¸ºè¿™äº›æ–°å…ƒç´ çš„æ¸¸ç¨‹é•¿åº¦ï¼Œé‚£ä¹ˆè¿™å®é™…ä¸Šæ˜¯ä¸€ä¸ªå¯æ¥å—çš„ RLEâ€”â€”å®Œå…¨æ²¡æœ‰æŸåï¼
- æœ€åï¼Œâ€œreversedâ€å±æ€§æ„å‘³ç€åˆ—è¡¨çš„å‰é¢å’Œåé¢éƒ½å¿…é¡»ä»¥ç›¸åŒçš„æ–¹å¼è¿›è¡Œä¿®æ”¹ã€‚ä¸ºäº†é¿å…ä¸¤æ¬¡æŸåç›¸åŒçš„å…ƒç´ ï¼Œæˆ‘ä»¬è¦æ±‚åˆ—è¡¨è‡³å°‘æœ‰å››ä¸ªå…ƒç´ ã€‚

å°†æ‰€æœ‰è¿™äº›è¦æ±‚æ”¾åœ¨ä¸€èµ·ï¼Œæˆ‘ä»¬å¯ä»¥æå‡ºè¿™ä¸ªå®ç°ï¼Œå…¶ä¸­ `rle_recursive` æ˜¯[ä¸Šä¸€ç¯‡æ–‡ç« ](https://fsharpforfunandprofit.com/posts/return-of-the-edfh-2#correct1)ä¸­æ­£ç¡®çš„ RLE å®ç°ã€‚

```F#
/// An incorrect implementation that satisfies all the properties
let rle_corrupted (inputStr:string) : (char*int) list =

  // helper
  let duplicateFirstTwoChars list =
    match list with
    | (ch1,n)::(ch2,m)::e3::e4::tail when n > 1 && m > 1 ->
      (ch1,1)::(ch2,1)::(ch1,n-1)::(ch2,m-1)::e3::e4::tail
    | _ ->
      list

  // start with correct output...
  let output = rle_recursive inputStr

  // ...and then corrupt it by
  // adding extra chars front and back
  output
  |> duplicateFirstTwoChars
  |> List.rev
  |> duplicateFirstTwoChars
  |> List.rev
```

è¯·æ³¨æ„ï¼Œæˆ‘ä»¬ä»…åœ¨ä»¥ä¸‹æƒ…å†µä¸‹æŸååˆ—è¡¨ï¼š

- å‰ä¸¤æ¬¡çš„è¡Œç¨‹é•¿åº¦å¤§äº 1ï¼Œæ‰€ä»¥æˆ‘ä»¬å¯ä»¥å· 1ã€‚
- åˆ—è¡¨ä¸­è‡³å°‘æœ‰å››ä¸ªå…ƒç´ ï¼Œè¿™æ ·æˆ‘ä»¬å°±å¯ä»¥åè½¬å¹¶é‡æ–°æŸåå¦ä¸€ç«¯ã€‚å¦‚æœåˆ é™¤ `::e3::e4::` ä¸Šçš„åŒ¹é…é¡¹ï¼Œåˆ™å®ç°å°†ä½¿â€œreversedâ€å±æ€§å¤±è´¥ã€‚

é‚£ä¹ˆï¼Œå½“æˆ‘ä»¬è¿è¡Œæ£€æŸ¥å™¨æ—¶ï¼Œæˆ‘ä»¬å¸Œæœ›å¢åŠ æµ‹è¯•çš„æ•°é‡ï¼Œå› ä¸ºåªæœ‰å°‘æ•°è¾“å…¥ç¬¦åˆæŸåçš„è¦æ±‚ï¼Œæˆ‘ä»¬å¸Œæœ›ç¡®ä¿æ•è·åˆ°å®ƒä»¬ã€‚

å¥½çš„ï¼Œè®©æˆ‘ä»¬å¯¹ç…§ä¸Šæ¬¡å®šä¹‰çš„å¤åˆå±æ€§ `propRle` æ¥æ£€æŸ¥è¿™ä¸ªæ–°çš„ EDFH å®ç°ã€‚å¦‚å‰æ‰€è¿°ï¼Œæˆ‘ä»¬å°†ä½¿ç”¨è‡ªå®šä¹‰ç”Ÿæˆå™¨ `arbPixels` æ¥ç”ŸæˆåŒ…å«å¤§é‡è¿è¡Œçš„å­—ç¬¦ä¸²ã€‚

```F#
let prop = Prop.forAll arbPixels (propRle rle_corrupted)

// check it thoroughly
let config = { Config.Default with MaxTest=10000}
Check.One(config,prop)
// Ok, passed 10000 tests.
```

å®ƒé€šè¿‡äº†ã€‚å“¦ï¼Œå¤©å“ªï¼æˆ‘ä»¬ç°åœ¨è¯¥å¦‚ä½•å‡»è´¥ EDFHï¼Ÿ

åœ¨ä¹‹å‰çš„ä¸€ç¯‡æ–‡ç« ä¸­ï¼Œæˆ‘æè¿°äº†ä¸€äº›å¯ç”¨äºç”Ÿæˆå±æ€§çš„æ–¹æ³•ã€‚æˆ‘ä»¬å·²ç»ä½¿ç”¨å…¶ä¸­ä¸€ä¸ªï¼ˆâ€œæœ‰äº›ä¸œè¥¿æ°¸è¿œä¸ä¼šæ”¹å˜â€ï¼‰æ¥è¦æ±‚ä¸€ä¸ªä¸å˜é‡ï¼Œå³æºå­—ç¬¦ä¸²ä¸­çš„æ¯ä¸ªå­—ç¬¦ä¹Ÿå‡ºç°åœ¨ RLE ä¸­ã€‚æˆ‘ä»¬å°†åœ¨è¿™ç¯‡æ–‡ç« ä¸­ä½¿ç”¨å…¶ä¸­çš„ä¸¤ä¸ªï¼š

- â€œä¸åŒçš„è·¯å¾„ï¼Œç›¸åŒçš„ç›®çš„åœ°â€
- â€œæ¥å›â€

## ç»“æ„ä¿æŒæ€§èƒ½æµ‹è¯•

å¯¹äºæˆ‘ä»¬çš„ç¬¬ä¸€ç§æ–¹æ³•ï¼Œæˆ‘ä»¬å°†ä½¿ç”¨â€œäº¤æ¢å›¾â€æ–¹æ³•çš„å˜ä½“ã€‚

![img](https://fsharpforfunandprofit.com/posts/return-of-the-edfh-3/property_commutative.png)

åœ¨è¿™ç§æƒ…å†µä¸‹ï¼Œæˆ‘ä»¬å°†åˆ©ç”¨æ¸¸ç¨‹ç¼–ç æ˜¯ä¸€ç§â€œç»“æ„ä¿æŒâ€æ“ä½œçš„äº‹å®ã€‚è¿™æ„å‘³ç€â€œå­—ç¬¦ä¸²ä¸–ç•Œâ€ä¸­çš„æ“ä½œåœ¨è½¬æ¢ä¸ºâ€œRLE ä¸–ç•Œâ€åå¾—ä»¥ä¿ç•™ã€‚

å¯¹å­—ç¬¦ä¸²çš„å®šä¹‰æ“ä½œæ˜¯ä¸²è”ï¼ˆå› ä¸ºå­—ç¬¦ä¸²æ˜¯[å¹ºåŠç¾¤](https://fsharpforfunandprofit.com/posts/monoids-without-tears/)ï¼‰ï¼Œå› æ­¤æˆ‘ä»¬è¦æ±‚å¯¹å­—ç¬¦ä¸²çš„ç»“æ„ä¿æŒæ“ä½œå°†å­—ç¬¦ä¸²ä¸–ç•Œä¸­çš„ä¸²è”æ˜ å°„åˆ°ç›®æ ‡ä¸–ç•Œä¸­çš„çº§è”

`OP(str1 + str2) = OP(str1) + OP(str2)`

`strLen` æ˜¯ä¸€ä¸ªå¯¹å­—ç¬¦ä¸²è¿›è¡Œç®€å•ç»“æ„ä¿ç•™æ“ä½œçš„ç¤ºä¾‹ã€‚å®ƒä¸ä»…ä»…æ˜¯å­—ç¬¦ä¸²åˆ°æ•´æ•°çš„éšæœºæ˜ å°„ï¼Œå› ä¸ºå®ƒä¿ç•™äº†è¿æ¥æ“ä½œã€‚

`strLen(str1 + str2) = strLen(str1) + strLen(str2)`

å€¼å¾—æ³¨æ„çš„æ˜¯ï¼Œâ€œç»“æ„ä¿ç•™â€å¹¶ä¸æ„å‘³ç€å®ƒä¿ç•™äº†å­—ç¬¦ä¸²çš„å†…å®¹ï¼Œåªæ˜¯ä¿ç•™äº†å­—ç¬¦ä¸²ä¹‹é—´çš„å…³ç³»ã€‚ä¸Šé¢çš„ `strLen` å‡½æ•°ä¸ä¿ç•™å­—ç¬¦ä¸²çš„å†…å®¹ï¼Œä½ ç”šè‡³å¯ä»¥æœ‰ä¸€ä¸ª `empty` å‡½æ•°ï¼Œå°†æ‰€æœ‰å­—ç¬¦ä¸²æ˜ å°„åˆ°ä¸€ä¸ªç©ºåˆ—è¡¨ä¸Šã€‚å®ƒä¸ä¼šä¿ç•™å†…å®¹ï¼Œä½†ä¼šä¿ç•™ç»“æ„ï¼Œå› ä¸ºï¼š

`empty(str1 + str2) = empty(str1) + empty(str2)`

åœ¨æˆ‘ä»¬çš„ä¾‹å­ä¸­ï¼Œæˆ‘ä»¬å¸Œæœ› `rle` å‡½æ•°ä¹Ÿèƒ½ä¿ç•™å­—ç¬¦ä¸²çš„ç»“æ„ï¼Œè¿™æ„å‘³ç€æˆ‘ä»¬éœ€è¦ï¼š

`rle(str1 + str2) = rle(str1) + rle(str2)`

æ‰€ä»¥ç°åœ¨æˆ‘ä»¬éœ€è¦çš„æ˜¯ä¸€ç§â€œæ·»åŠ â€ä¸¤ä¸ª `Rle` ç»“æ„çš„æ–¹æ³•ã€‚å³ä½¿å®ƒä»¬æ˜¯åˆ—è¡¨ï¼Œæˆ‘ä»¬ä¹Ÿä¸èƒ½ç›´æ¥å°†å®ƒä»¬è¿æ¥èµ·æ¥ï¼Œå› ä¸ºæˆ‘ä»¬æœ€ç»ˆå¯èƒ½ä¼šå¾—åˆ°ç›¸é‚»çš„è¿è¡Œã€‚ç›¸åï¼Œæˆ‘ä»¬å¸Œæœ›åˆå¹¶ç›¸åŒå­—ç¬¦çš„è¿è¡Œï¼š

```F#
// wrong
['a',1] + ['a',1]  //=> [('a',1); ('a',1)]
// correct
['a',1] + ['a',1]  //=> [('a',2)]
```

ä¸‹é¢æ˜¯è¿™æ ·ä¸€ä¸ªå‡½æ•°çš„å®ç°ã€‚æ‰€æœ‰ç‰¹æ®Šæƒ…å†µéƒ½æœ‰ç‚¹æ£˜æ‰‹ã€‚

```F#
// A Rle is a list of chars and run-lengths
type Rle = (char*int) list

let rec rleConcat (rle1:Rle) (rle2:Rle) =
  match rle1 with
  // 0 elements, so return rle2
  | [] -> rle2

  // 1 element left, so compare with
  // first element of rle2 and merge if equal
  | [ (x,xCount) ] ->
    match rle2 with
    | [] ->
      rle1
    | (y,yCount)::tail ->
      if x = y then
        // merge
        (x,(xCount+yCount)) :: tail
      else
        (x,xCount)::(y,yCount)::tail

  // longer than 1, so recurse
  | head::tail ->
    head :: (rleConcat tail rle2)
```

ä¸€äº›äº¤äº’å¼æµ‹è¯•ï¼Œä»¥ç¡®ä¿å®ƒçœ‹èµ·æ¥ä¸é”™ï¼š

```F#
rleConcat ['a',1] ['a',1]  //=> [('a',2)]
rleConcat ['a',1] ['b',1]  //=> [('a',1); ('b',1)]

let rle1 = rle_recursive "aaabb"
let rle2 = rle_recursive "bccc"
let rle3 = rle_recursive ("aaabb" + "bccc")
rle3 = rleConcat rle1 rle2   //=> true
```

æˆ‘ä»¬ç°åœ¨æœ‰äº† RLE çš„â€œconcatâ€å‡½æ•°ï¼Œæ‰€ä»¥æˆ‘ä»¬å¯ä»¥å®šä¹‰ä¸€ä¸ªå±æ€§æ¥æ£€æŸ¥ RLE å®ç°æ˜¯å¦ä¿ç•™äº†å­—ç¬¦ä¸²è¿æ¥ã€‚

```F#
let propConcat (impl:RleImpl) (str1,str2) =
  let ( <+> ) = rleConcat

  let rle1 = impl str1
  let rle2 = impl str2
  let actual = rle1 <+> rle2
  let expected = impl (str1 + str2)
  actual = expected
```

æ­¤å±æ€§éœ€è¦ä¸€å¯¹å­—ç¬¦ä¸²ï¼Œè€Œä¸ä»…ä»…æ˜¯ä¸€ä¸ªï¼Œå› æ­¤æˆ‘ä»¬éœ€è¦åˆ›å»ºä¸€ä¸ªæ–°çš„ç”Ÿæˆå™¨ï¼š

```F#
let arbPixelsPair =
  arbPixels.Generator
  |> Gen.two
  |> Arb.fromGen
```

æœ€åï¼Œæˆ‘ä»¬å¯ä»¥æ ¹æ®å±æ€§æ£€æŸ¥ EDFH çš„å®ç°ï¼š

```F#
let prop = Prop.forAll arbPixelsPair (propConcat rle_corrupted)

// check it thoroughly
let config = { Config.Default with MaxTest=10000}
Check.One(config,prop)
// Falsifiable, after 2 tests
```

å¤±è´¥äº†ï¼ä½†æ­£ç¡®çš„å®ç°ä»ç„¶æˆåŠŸï¼š

```F#
let prop = Prop.forAll arbPixelsPair (propConcat rle_recursive)

// check it thoroughly
let config = { Config.Default with MaxTest=10000}
Check.One(config,prop)
// Ok, passed 10000 tests.
```

è®©æˆ‘ä»¬å¤ä¹ ä¸€ä¸‹ã€‚æˆ‘ä»¬å¯ä»¥ç”¨æ›´é€šç”¨çš„â€œconcat ä¿æŒâ€å±æ€§æ›¿æ¢è¿‡åº¦çº¦æŸçš„â€œreverseâ€å±æ€§ï¼Œè¿™æ ·æˆ‘ä»¬çš„è¡Œç¨‹ç¼–ç å®ç°çš„å±æ€§å¦‚ä¸‹ï¼š

- **å†…å®¹ä¸å˜æ€§**ï¼šè¾“å‡ºå¿…é¡»ä»¥ç›¸åŒçš„é¡ºåºåŒ…å«è¾“å…¥ä¸­çš„æ‰€æœ‰å­—ç¬¦ã€‚
- **æ¸¸ç¨‹ä¸åŒ**ï¼šè¾“å‡ºä¸­çš„ä¸¤ä¸ªç›¸é‚»å­—ç¬¦ä¸èƒ½ç›¸åŒã€‚
- **æ€»é•¿åº¦ç›¸åŒ**ï¼šè¾“å‡ºä¸­çš„æ¸¸ç¨‹é•¿åº¦ä¹‹å’Œå¿…é¡»ç­‰äºè¾“å…¥çš„æ€»é•¿åº¦ã€‚
- **ç»“æ„ä¿æŒ**ï¼šå¿…é¡»ä¿æŒå¦‚ä¸Šæ‰€è¿°çš„è¿æ¥ã€‚

æˆ‘ä»¬æœ‰å››ä¸ªå•ç‹¬çš„å±æ€§ï¼Œæ¯ä¸ªå±æ€§éƒ½å¿…é¡»å•ç‹¬å‘ç°å’Œå®ç°ã€‚æœ‰æ›´ç®€å•çš„æ–¹æ³•å—ï¼Ÿæ˜¯çš„ï¼Œæœ‰ï¼

## ä½¿ç”¨é€†å‡½æ•°è¿›è¡Œæµ‹è¯•

å¦‚æœæˆ‘ä»¬å›åˆ°è¡Œç¨‹ç¼–ç çš„ç›®çš„ï¼Œå®ƒåº”è¯¥ä»¥å‹ç¼©ä½†æ— æŸçš„æ–¹å¼è¡¨ç¤ºå­—ç¬¦ä¸²ã€‚â€œæ— æŸâ€æ˜¯å…³é”®ã€‚è¿™æ„å‘³ç€æˆ‘ä»¬æœ‰ä¸€ä¸ªé€†å‡½æ•°â€”â€”ä¸€ä¸ªå¯ä»¥ä» RLE æ•°æ®ç»“æ„ä¸­é‡å»ºåŸå§‹å­—ç¬¦ä¸²çš„å‡½æ•°ã€‚

ç”±äºæˆ‘ä»¬æœ‰ä¸€ä¸ªé€†ï¼Œæˆ‘ä»¬å¯ä»¥è¿›è¡Œâ€œå‰åâ€æµ‹è¯•ã€‚ç¼–ç å’Œè§£ç åº”è¯¥è®©æˆ‘ä»¬å›åˆ°èµ·ç‚¹ã€‚

![img](https://fsharpforfunandprofit.com/posts/return-of-the-edfh-3/property_inverse.png)

åœ¨æˆ‘ä»¬å¼€å§‹å¤„ç†è§£ç å™¨ä¹‹å‰ï¼Œè®©æˆ‘ä»¬åœæ­¢å¹¶ä¸º RLE ç¼–ç å®šä¹‰ä¸€ä¸ªé€‚å½“çš„ç±»å‹ï¼Œä»¥ä¾¿æˆ‘ä»¬å¯ä»¥å¯¹å…¶è¿›è¡Œä¸€äº›å°è£…ã€‚è¿™å°†åœ¨ä»¥åè¯æ˜æ˜¯æœ‰ç”¨çš„ã€‚

```F#
type Rle = Rle of (char*int) list
```

ç°åœ¨ï¼Œæˆ‘ä»¬åº”è¯¥è¿”å›å¹¶æ›´æ”¹ä¹‹å‰çš„â€œç¼–ç â€å®ç°ï¼Œä»¥ç«‹å³è¿”å› `Rle`ã€‚æˆ‘æŠŠå®ƒå½“ä½œç»ƒä¹ ã€‚

å®ç°ä¸€ä¸ªæ¥å— `Rle` å¹¶è¿”å› `string` çš„è§£ç å™¨å¾ˆç®€å•ã€‚æœ‰å¾ˆå¤šæ–¹æ³•å¯ä»¥åšåˆ°è¿™ä¸€ç‚¹ã€‚æˆ‘é€‰æ‹©ä½¿ç”¨å¯å˜ `StringBuilder` å’ŒåµŒå¥—å¾ªç¯æ¥æé«˜æ€§èƒ½ã€‚

```F#
let decode (Rle rle) : string =
  let sb = System.Text.StringBuilder()
  for (ch,count) in rle do
    for _ in [1..count] do
      sb.Append(ch) |> ignore
  sb.ToString()
```

è®©æˆ‘ä»¬ä»¥äº¤äº’æ–¹å¼å¿«é€Ÿæµ‹è¯•ä¸€ä¸‹ï¼š

```F#
rle_recursive "111000011"
|> Rle     // wrap in Rle type
|> decode  //=> "111000011"
```

å¥½çš„ï¼Œè¿™ä¼¼ä¹æœ‰æ•ˆã€‚æˆ‘ä»¬å¯ä»¥åˆ›å»ºä¸€ç³»åˆ—å±æ€§ï¼Œåˆ†åˆ«ç”¨äºæµ‹è¯• `decode` å’Œç¼–ç ï¼Œä½†ç°åœ¨æˆ‘ä»¬åªå°†å®ƒä»¬ä½œä¸ºä¸€å¯¹é€†è¿›è¡Œæµ‹è¯•ã€‚

æœ‰äº† `decode` å‡½æ•°ï¼Œæˆ‘ä»¬å¯ä»¥ç¼–å†™â€œthere and back againâ€å±æ€§ï¼š

```F#
let propEncodeDecode (encode:RleImpl) inputStr =
  let actual =
    inputStr
    |> encode
    |> Rle  // wrap in Rle type
    |> decode

  actual = inputStr
```

è®©æˆ‘ä»¬å¯¹ç…§ EDFH çš„é”™è¯¯å®ç°æ¥æ£€æŸ¥è¿™ä¸ªå±æ€§ï¼Œå®ƒå¤±è´¥äº†ã€‚æ°å‡ºçš„ï¼

```F#
let prop = Prop.forAll arbPixels (propEncodeDecode rle_corrupted)

// check it thoroughly
let config = { Config.Default with MaxTest=10000}
Check.One(config,prop)
// Falsifiable, after 2 tests
```

å¦‚æœæˆ‘ä»¬æ ¹æ®è‰¯å¥½çš„å®ç°æ£€æŸ¥è¿™ä¸ªå±æ€§ï¼Œå®ƒå°±ä¼šé€šè¿‡ã€‚

```F#
let prop = Prop.forAll arbPixels (propEncodeDecode rle_recursive)

// check it thoroughly
let config = { Config.Default with MaxTest=10000}
Check.One(config,prop)
// Ok, passed 10000 tests.
```

å› æ­¤ï¼Œæˆ‘ä»¬æœ€ç»ˆåˆ›å»ºäº†ä¸€ä¸ªå±æ€§ï¼Œå¯ä»¥å‡»è´¥â€œæŸåâ€çš„ EDFH å‡½æ•°ã€‚

ç„¶è€Œï¼Œæˆ‘ä»¬è¿˜æ²¡æœ‰å®Œæˆï¼Œå› ä¸ºä¹‹å‰çš„ EDFH å®ç°ä¹‹ä¸€ç¡®å®æ»¡è¶³äº†è¿™ä¸ªå±æ€§ï¼Œå³æœ€ç®€å•çš„ `rle_allChars`ã€‚

```F#
/// a very simple RLE implementation
let rle_allChars inputStr =
  inputStr
  |> Seq.toList
  |> List.map (fun ch -> (ch,1))

// make a property 
let prop = Prop.forAll arbPixels (propEncodeDecode rle_allChars)

// and check it thoroughly
let config = { Config.Default with MaxTest=10000}
Check.One(config,prop)
// Ok, passed 10000 tests.
```

è¿™æ˜¯å› ä¸ºå®ƒæ˜¯ä¸€ä¸ªæ­£ç¡®çš„æ¸¸ç¨‹ç¼–ç ï¼Œè€Œä¸æ˜¯æœ€å¤§æ¸¸ç¨‹ç¼–ç ï¼

## å®šä¹‰ RLE è§„èŒƒ

åœ¨ç¬¬ä¸€ç¯‡æ–‡ç« ä¸­ï¼Œæˆ‘æåˆ°æˆ‘æ— æ³•å¿«é€Ÿæ‰¾åˆ° RLE å®ç°çš„ç¨‹åºå‘˜å‹å¥½è§„èŒƒã€‚æˆ‘è®¤ä¸ºæˆ‘ä»¬ç°åœ¨å·²ç»å—å¤Ÿäº†ã€‚

é¦–å…ˆï¼ŒRLE æ˜¯æ— æŸçš„ï¼Œæ‰€ä»¥æˆ‘ä»¬å¯ä»¥è¯´ä¹Ÿå¿…é¡»æœ‰ä¸€ä¸ªé€†å‡½æ•°ã€‚å³ä½¿æ²¡æœ‰ç¡®åˆ‡å®šä¹‰é€†å‡½æ•°æ˜¯ä»€ä¹ˆï¼Œæˆ‘ä»¬ä¹Ÿå¯ä»¥è¯´â€œå¾€è¿”æ€§è´¨â€æˆç«‹ã€‚

å…¶æ¬¡ï¼Œæˆ‘ä»¬éœ€è¦æ¶ˆé™¤çç¢çš„ç¼–ç ï¼Œä¾‹å¦‚æ¯ä¸ªæ¸¸ç¨‹é•¿åº¦ä¸º 1 çš„ç¼–ç ã€‚æˆ‘ä»¬å¯ä»¥é€šè¿‡è¦æ±‚æ¸¸ç¨‹ä¸ºæœ€å¤§å€¼æ¥å®ç°è¿™ä¸€ç‚¹ï¼Œè¿™æ„å‘³ç€ç›¸é‚»çš„æ¸¸ç¨‹ä¸å…±äº«ç›¸åŒçš„å­—ç¬¦ã€‚

æˆ‘è®¤ä¸ºè¿™å°±æ˜¯æˆ‘ä»¬æ‰€éœ€è¦çš„ã€‚å…¶ä»–å±æ€§æ˜¯éšå«çš„ã€‚ä¾‹å¦‚ï¼Œç”±äºå¾€è¿”å±æ€§ï¼Œâ€œåŒ…å«è¾“å…¥ä¸­çš„æ‰€æœ‰å­—ç¬¦â€æ˜¯éšå«çš„ã€‚å‡ºäºåŒæ ·çš„åŸå› ï¼Œä¹Ÿéšå«äº†â€œè¡Œç¨‹é•¿åº¦ä¹‹å’Œâ€å±æ€§ã€‚

æ‰€ä»¥ï¼Œè¿™æ˜¯è§„æ ¼ï¼š

> RLEå®ç°æ˜¯ä¸€å¯¹å‡½æ•° `encode : string->Rle` å’Œ `decode : Rle->string`ï¼Œè¿™æ ·ï¼š
>
> - **å¾€è¿”**ã€‚ç”± `decode` ç»„æˆçš„ `encode` ä¸æ’ç­‰å‡½æ•°ç›¸åŒã€‚
> - **æœ€å¤§æ¸¸ç¨‹æ¬¡æ•°**ã€‚Rle ç»“æ„ä¸­æ²¡æœ‰ç›¸é‚»çš„æ¸¸ç¨‹å…±äº«ç›¸åŒçš„å­—ç¬¦ï¼Œå¹¶ä¸”æ‰€æœ‰æ¸¸ç¨‹é•¿åº¦éƒ½å¤§äº 0ã€‚

ä½ èƒ½æƒ³å‡ºä¸€ç§æ–¹æ³•è®© EDFH æ‰“ç ´è¿™ä¸ªè§„èŒƒå—ï¼Ÿè¯·åœ¨è¯„è®ºä¸­å‘Šè¯‰æˆ‘ã€‚

## å¥–åŠ±ï¼šèµ°å‘å¦ä¸€ä¸ªæ–¹å‘

æˆ‘ä»¬å¯ä»¥å°±æ­¤æ­¢æ­¥ï¼Œä½†è®©æˆ‘ä»¬è¿›ä¸€æ­¥æ¢ç´¢ FsCheckã€‚

ç¼–ç å’Œè§£ç æ˜¯å½¼æ­¤çš„é€†ï¼Œæ‰€ä»¥æˆ‘ä»¬åŒæ ·å¯ä»¥å®šä¹‰ä¸€ä¸ªä»è§£ç å¼€å§‹çš„å±æ€§ï¼Œç„¶åå¯¹ç»“æœè¿›è¡Œç¼–ç ï¼Œå¦‚ä¸‹æ‰€ç¤ºï¼š

```F#
let propDecodeEncode (encode:RleImpl) rle =
  let actual =
    rle
    |> decode
    |> encode
    |> Rle

  actual = rle
```

å¦‚æœæˆ‘ä»¬é’ˆå¯¹ EDFH æŸåçš„ç¼–ç å™¨è¿›è¡Œæµ‹è¯•ï¼Œå®ƒå°†å¤±è´¥ï¼š

```F#
let prop = propDecodeEncode rle_corrupted
Check.Quick(prop)
// Falsifiable, after 4 tests
// Rle [('a', 0)]
```

ä½†å®ƒä¹Ÿæ— æ³•ä½¿ç”¨æˆ‘ä»¬æ­£ç¡®çš„ `rle_recursive` ç¼–ç å™¨ã€‚

```F#
let prop = propDecodeEncode rle_recursive
Check.Quick(prop)
// Falsifiable, after 4 tests
// Rle [('a', 0)]
```

ä¸ºä»€ä¹ˆï¼Ÿæˆ‘ä»¬å¯ä»¥ç«‹å³çœ‹åˆ° FsCheck æ­£åœ¨ç”Ÿæˆä¸€ä¸ª 0 é•¿åº¦çš„æ¸¸ç¨‹ï¼Œå½“è§£ç å’Œç¼–ç æ—¶ï¼Œå®ƒå°†è¿”å›ä¸€ä¸ªç©ºåˆ—è¡¨ã€‚ä¸ºäº†è§£å†³è¿™ä¸ªé—®é¢˜ï¼Œæˆ‘ä»¬å¿…é¡»å†æ¬¡åˆ›å»ºè‡ªå·±çš„ç”Ÿæˆå™¨ã€‚

### è§‚å¯Ÿæœ‰è¶£çš„ RLE

ä¸è¿‡ï¼Œåœ¨æˆ‘ä»¬åˆ›å»ºæ–°çš„ç”Ÿæˆå™¨ä¹‹å‰ï¼Œè®©æˆ‘ä»¬è¿›è¡Œä¸€äº›ç›‘æ§ï¼Œä»¥ä¾¿æˆ‘ä»¬å¯ä»¥åˆ¤æ–­å®ƒæ˜¯å¦çœŸçš„åœ¨å·¥ä½œã€‚

æˆ‘ä»¬å°†éµå¾ªä¸ä»¥å‰ç›¸åŒçš„æ–¹æ³•ã€‚é¦–å…ˆï¼Œæˆ‘ä»¬å°†å®šä¹‰â€œæœ‰è¶£â€çš„æ ·å­ï¼Œç„¶åæˆ‘ä»¬å°†åˆ›å»ºä¸€ä¸ªè™šæ‹Ÿå±æ€§æ¥ç›‘è§†è¾“å…¥ã€‚

é¦–å…ˆï¼Œæˆ‘ä»¬ä¼šè¯´ä¸€ä¸ªâ€œæœ‰è¶£â€çš„ RLE æ˜¯ä¸€ä¸ªé•¿åº¦éå¹³å‡¡ä¸”åŒ…å«ä¸€äº›éå¹³å‡¡æ¸¸ç¨‹çš„ RLEã€‚

```F#
let isInterestingRle (Rle rle) =
  let isLongList = rle.Length > 2
  let noOfLongRuns =
    rle
    |> List.filter (fun (_,run) -> run > 2)
    |> List.length
  isLongList && (noOfLongRuns > 2)
```

ç„¶åè®©æˆ‘ä»¬ç”¨å®ƒæ¥å¯¹å±æ€§çš„è¾“å…¥è¿›è¡Œåˆ†ç±»ï¼š

```F#
let propIsInterestingRle input =
  let isInterestingInput = isInterestingRle input

  true // we don't care about the actual test
  |> Prop.classify (not isInterestingInput) "not interesting"
  |> Prop.classify isInterestingInput "interesting"
```

ç»“æœå¾ˆæ˜æ˜¾â€”â€”FsCheck è‡ªåŠ¨ç”Ÿæˆçš„å¤§å¤šæ•°è¾“å…¥éƒ½æ˜¯æ— è¶£çš„ã€‚

```F#
Check.Quick propIsInterestingRle
// Ok, passed 100 tests.
// 99% not interesting.
// 1% interesting.
```

### ç”Ÿæˆæœ‰è¶£çš„ RLE

é‚£ä¹ˆï¼Œè®©æˆ‘ä»¬å»ºé€ ä¸€ä¸ªç”Ÿæˆå™¨ã€‚æˆ‘ä»¬å°†é€‰å–ä¸€ä¸ªéšæœºå­—ç¬¦å’Œä¸€ä¸ªéšæœºæ¸¸ç¨‹é•¿åº¦ï¼Œå¹¶å°†å®ƒä»¬ç»„åˆæˆä¸€å¯¹ï¼Œå¦‚ä¸‹æ‰€ç¤ºï¼š

```F#
let arbRle =
  let genChar = Gen.elements ['a'..'z']
  let genRunLength = Gen.choose(1,10)
  Gen.zip genChar genRunLength
  |> Gen.listOf
  |> Gen.map Rle
  |> Arb.fromGen
```

å¦‚æœæˆ‘ä»¬ä½¿ç”¨è¿™ä¸ªæ–°çš„ç”Ÿæˆå™¨æ£€æŸ¥å±æ€§ï¼Œç°åœ¨çš„ç»“æœè¦å¥½å¾—å¤šï¼š

```F#
let prop = Prop.forAll arbRle propIsInterestingRle
Check.Quick prop
// Ok, passed 100 tests.
// 86% interesting.
// 14% not interesting.
```

è®©æˆ‘ä»¬ç”¨è¿™ä¸ªæ–°çš„ç”Ÿæˆå™¨é‡æ–°æµ‹è¯•æˆ‘ä»¬çš„æ­£ç¡®å®ç°ã€‚

```F#
let prop = Prop.forAll arbRle (propDecodeEncode rle_recursive)

// check it thoroughly
let config = { Config.Default with MaxTest=10000}
Check.One(config,prop)
// Falsifiable, after 82 tests
// Rle [('e', 7); ('e', 6); ('z', 10)]
```

å“å‘€ï¼Œæˆ‘ä»¬åˆåšåˆ°äº†ã€‚å®ƒä»ç„¶å¤±è´¥äº†ã€‚

å¹¸è¿çš„æ˜¯ï¼Œåä¾‹å‘æˆ‘ä»¬å±•ç¤ºäº†åŸå› ã€‚ä¸¤ä¸ªç›¸é‚»çš„å­—ç¬¦æ˜¯ç›¸åŒçš„ï¼Œè¿™æ„å‘³ç€é‡æ–°ç¼–ç å°†ä¸åŸå§‹å­—ç¬¦ä¸åŒ¹é…ã€‚å¯¹æ­¤çš„ä¿®å¤æ˜¯è¿‡æ»¤æ‰ç”Ÿæˆå™¨é€»è¾‘ä¸­çš„è¿™äº›å…±äº«å­—ç¬¦è¿è¡Œã€‚

ä»¥ä¸‹æ˜¯åˆ é™¤ç›¸é‚»è¿è¡Œçš„ä»£ç ï¼š

```F#
let removeAdjacentRuns runList =
  let folder prevRuns run =
    match prevRuns with
    | [] -> [run]
    | head::_ ->
      if fst head <> fst run then
        // add
        run::prevRuns
      else
        // duplicate -- ignore
        prevRuns
  runList
  |> List.fold folder []
  |> List.rev
```

è¿™æ˜¯æ›´æ–°çš„ç”Ÿæˆå™¨ï¼š

```F#
let arbRle =
  let genChar = Gen.elements ['a'..'z']
  let genRunLength = Gen.choose(1,10)
  Gen.zip genChar genRunLength
  |> Gen.listOf
  |> Gen.map removeAdjacentRuns
  |> Gen.map Rle
  |> Arb.fromGen
```

ç°åœ¨ï¼Œå¦‚æœæˆ‘ä»¬å†æµ‹è¯•ä¸€æ¬¡ï¼Œä¸€åˆ‡æ­£å¸¸ã€‚

```F#
let prop = Prop.forAll arbRle (propDecodeEncode rle_recursive)

// check it thoroughly
let config = { Config.Default with MaxTest=10000}
Check.One(config,prop)
// Ok, passed 10000 tests.
```

## ä¸º RLE æ³¨å†Œç”Ÿæˆå™¨

FsCheck ä¸ºæ‰€æœ‰å¸¸è§ç±»å‹ï¼ˆ`string`ã€`int` ç­‰ï¼‰å®šä¹‰äº†é»˜è®¤ç”Ÿæˆå™¨ï¼Œä¹Ÿå¯ä»¥é€šè¿‡åå°„ä¸ºå¤åˆç±»å‹ï¼ˆè®°å½•ã€åˆ¤åˆ«è”åˆï¼‰ç”Ÿæˆæ•°æ®ï¼Œä½†æ­£å¦‚æˆ‘ä»¬æ‰€è§ï¼Œæˆ‘ä»¬é€šå¸¸éœ€è¦æ›´å¤šçš„æ§åˆ¶ã€‚

åˆ°ç›®å‰ä¸ºæ­¢ï¼Œæˆ‘ä»¬ä¸€ç›´åœ¨ä½¿ç”¨ `Prop.forAll` å°† `arbRle` å®ä¾‹æ˜¾å¼ä¼ é€’åˆ°æ¯ä¸ªæµ‹è¯•ä¸­ã€‚FsCheck æ”¯æŒä¸ºç±»å‹æ³¨å†Œ `Arbitrary`ï¼Œè¿™æ ·æ‚¨å°±ä¸å¿…æ¯æ¬¡éƒ½ä¼ é€’å®ƒã€‚å¯¹äºä¸€ç§ä¼šè¢«å¤§é‡é‡ç”¨çš„å¸¸è§ç±»å‹ï¼Œè¿™éå¸¸æ–¹ä¾¿ã€‚

FsCheck æä¾›äº†è®¸å¤šæœ‰ç”¨çš„å†…ç½®ç±»å‹å’Œè‡ªå®šä¹‰ç”Ÿæˆå™¨ï¼Œå¦‚ [PositiveInt](https://fscheck.github.io/FsCheck/reference/fscheck-positiveint.html)ã€[NonWhiteSpaceString](https://fscheck.github.io/FsCheck/reference/fscheck-nonwhitespacestring.html) ç­‰ï¼ˆè¯¦è§ [FsCheck å‘½åç©ºé—´](https://fscheck.github.io/FsCheck/reference/fscheck.html)ï¼‰ã€‚æˆ‘ä»¬å¦‚ä½•å°†è‡ªå®šä¹‰ç±»å‹æ·»åŠ åˆ°æ­¤åˆ—è¡¨ä¸­ï¼Ÿ

[FsCheck æ–‡æ¡£è§£é‡Šäº†å¦‚ä½•æ“ä½œ](https://fscheck.github.io/FsCheck//TestData.html#Default-Generators-and-Shrinkers-based-on-type)ã€‚æ‚¨é¦–å…ˆä¸ºè¦æ³¨å†Œçš„æ¯ä¸ª `Arbitrary` å®šä¹‰ä¸€ä¸ªå…·æœ‰é™æ€æ–¹æ³•çš„ç±»ï¼š

```F#
type MyGenerators =
  static member Rle() = arbRle

  // static member MyCustomType() = arbMyCustomType
```

ç„¶åå‘ FsCheck æ³¨å†Œè¯¥ç±»ï¼š

```F#
Arb.register<MyGenerators>()
```

æ³¨å†Œåï¼Œæ‚¨å¯ä»¥è·å¾—æ ·å“ï¼š

```F#
Arb.generate<Rle> |> Gen.sample 5 4
// [Rle [('c', 2); ('m', 8)];
//  Rle [];
//  Rle [('e', 7); ('c', 2); ('s', 1); ('m', 8)];
//  Rle [('t', 3); ('e', 7); ('c', 2)]]
```

æ— éœ€ `Prop.forAll` å³å¯æ£€æŸ¥å±æ€§ã€‚

```F#
let prop = propDecodeEncode rle_recursive

Check.Quick(prop)
// Ok, passed 100 tests.
```

## ç»“è®º

æœ¬ç³»åˆ—åˆ°æ­¤ç»“æŸã€‚æˆ‘ä»¬ä»ä¸€æ¡å…³äºå¦‚ä½•å›ç­”é¢è¯•é—®é¢˜çš„æ¨æ–‡å¼€å§‹ï¼Œè¿™æ˜¯ä¸€ç§æ„šè ¢çš„æ–¹å¼ï¼Œæœ€ç»ˆæˆ‘ä»¬ç»•é“è¿›å…¥ FsCheckï¼Œç¡®ä¿æˆ‘ä»¬æœ‰â€œæœ‰è¶£â€çš„è¾“å…¥ï¼Œæ„å»ºè‡ªå·±çš„ç”Ÿæˆå™¨ï¼Œå¹¶å°è¯•ä½¿ç”¨å±æ€§çš„ä¸åŒæ–¹å¼æ¥è·å¾—å¯¹å®ç°çš„ä¿¡å¿ƒã€‚

æˆ‘å¸Œæœ›è¿™ç»™äº†ä½ ä¸€äº›æƒ³æ³•ï¼Œå¯ä»¥ç”¨äºä½ è‡ªå·±çš„åŸºäºå±æ€§çš„æµ‹è¯•ã€‚ç©å¾—é«˜å…´ï¼

> æœ¬æ–‡ä¸­ä½¿ç”¨çš„æºä»£ç å¯ä»¥åœ¨[è¿™é‡Œæ‰¾åˆ°](https://github.com/swlaschin/fsharpforfunandprofit.com_code/tree/master/posts/return-of-the-edfh-3)ã€‚